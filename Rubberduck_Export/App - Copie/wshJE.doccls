Option Explicit

Private Sub Worksheet_Activate() '2024-03-03 @ 10:14

    Call GL_Trans_Import_All
    Call GL_JE_Auto_Import_All
    Call wshJE_Clear_All_Cells

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    'Within the form (E3:L24), force a tab order
    If Not Intersect(Target, Me.Range("E3:L24")) Is Nothing Then
        'Call Save_Detailled_Log("Entering Worksheet_SelectionChange (Form) - " & Target.Address)
        Dim tabOrderListOfCells As Variant, cellsAddress As Variant
        Dim addr As String
        Dim rng As Range, targetRng As Range
        
        If isTab_Order_Activated = True Then Exit Sub
        
        tabOrderListOfCells = Array("F4", "K4", "F6", "E9:K23")
        For Each cellsAddress In tabOrderListOfCells
            If rng Is Nothing Then
                Set rng = Range(cellsAddress)
            Else
                Set rng = Union(rng, Range(cellsAddress))
            End If
        Next
        
        Set targetRng = Intersect(rng, Target)
        rng.Select
        If targetRng Is Nothing Then
            addr = Target.Cells(1, 1).Address(ColumnAbsolute:=False, RowAbsolute:=False)
            cellsAddress = Application.Match(addr, tabOrderListOfCells, 0)
            If IsError(cellsAddress) Then
                'Call Save_Detailled_Log("Moving to cell = " & Range(tabOrderListOfCells(LBound(tabOrderListOfCells))).Address)
                Range(tabOrderListOfCells(LBound(tabOrderListOfCells))).Activate
            End If
        Else
            targetRng.Activate
        End If
        Call Save_Detailled_Log("wshJE - Exitting Worksheet_SelectionChange (Form) - " & Target.Address)
    End If
    
    'If date is empty, assign today's date by default
    If Not Intersect(Target, Me.Range("K4")) Is Nothing And Me.Range("K4").value = "" Then
        Me.Range("K4").value = Format(Now(), "dd-mm-yyyy")
        'Call Save_Detailled_Log("Entering Worksheet_SelectionChange (Date) - " & Me.Range("K4").value)
    End If
    
    'Clean memory
    On Error Resume Next
    Set rng = Nothing
    Set targetRng = Nothing
    On Error GoTo 0

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    'Si la cellule de la SOURCE change, alors on vérifie si l'on essaie d'appeler des E/J récurrentes
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        Call Save_Detailled_Log("Entering Worksheet_Change (Source) - " & Target.Address)
        Application.EnableEvents = False
        Dim source As String
        source = UCase(Trim(Range("F4").value))
        If source = "AUTO" Then
            wshJE.Range("B2").value = -1
            ufListeEJAuto.show
            If wshJE.Range("B2").value >= 0 Then
                wshJE.Range("K4").Activate
                wshJE.Range("K4").Select
            Else
                Call wshJE_Clear_All_Cells
            End If
        End If
        Application.EnableEvents = True
        Call Save_Detailled_Log("Exitting Worksheet_Change (Source) - " & Me.Range("F4").value)
    End If
    
    'Si la cellule de la DATE change, alors on essaie de construire une date
    If Not Intersect(Target, Range("K4")) Is Nothing Then
        Application.EnableEvents = False
        Call Save_Detailled_Log("Entering Worksheet_Change (Date) - " & Target.Address)
        Dim strDate As String
        strDate = Validate_A_Date(Me.Range("K4").value)
        If strDate = "" Then
            Call Invalid_Date_Message
            Me.Range("K4").Activate
            Me.Range("K4").Select
        Else
            Me.Range("K4").value = strDate
        End If
        If CDate(Me.Range("K4").value) > Format(Now(), "dd-mm-yyyy") Then
        Call Save_Detailled_Log("Dates comparaison: " & CDate(Me.Range("K4").value) & " vs. " & Format(Now(), "dd-mm-yyyy"))
            If MsgBox("En êtes-vous CERTAIN ?", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Me.Range("K4").value = ""
                Me.Range("K4").Activate
                Me.Range("K4").Select
            End If
        End If
        Call Save_Detailled_Log("Exitting Worksheet_Change (Date) - " & Me.Range("K4").value)
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, sauvegarde du no de compte & suggestion du montant résiduel
    If Not Intersect(Target, Range("E9:G23")) Is Nothing Then
'        Application.EnableEvents = False
        Call Save_Detailled_Log("Entering Worksheet_Change (Description) - " & Target.Address)
        With wshJE
            If .Range("E" & Target.row).value <> "" Then
                .Range("L" & Target.row).value = Get_GL_Code_From_GL_Description(.Range("E" & Target.row).value)
            End If
            
            If Target.row > 9 And _
                (.Range("H26").value <> 0 Or .Range("I26").value <> 0) And _
                .Range("H26").value <> .Range("I26").value Then
                    If .Range("H26").value > .Range("I26").value Then
                        .Range("I" & Target.row).value = .Range("H26").value - .Range("I26").value
                        .Range("I" & Target.row).Select
                    Else
                        .Range("H" & Target.row).value = .Range("I26").value - .Range("H26").value
                        .Range("H" & Target.row).Select
                    End If
            End If
        End With
        Call Save_Detailled_Log("Exitting Worksheet_Change (Description) - " & Me.Range("E" & Target.row).value)
'        Application.EnableEvents = True
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(Target, Range("H9:I23")) Is Nothing Then
        Call Save_Detailled_Log("Entering Worksheet_Change (Amount) - " & Target.Address)
        If (wshJE.Range("H" & Target.row).value < 0 Or wshJE.Range("I" & Target.row).value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H26").value <> Range("I26").value Then
            With Range("H26:I26").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("H26:I26").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
        Call Save_Detailled_Log("wshJE - Exitting Worksheet_Change (Amount) - " & wshJE.Range("H" & Target.row).value & " - " & wshJE.Range("I" & Target.row).value)
    End If
End Sub

Private Sub ckbRecurrente_Click()

    If ckbRecurrente.value = True Then
        ckbRecurrente.BackColor = vbYellow
    Else
        ckbRecurrente.BackColor = RGB(217, 217, 217)
    End If

End Sub
