Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)
Dim ProjRow As Long, CustRow As Long, ServItemDBRow As Long
'On Billing Entry Customer Change, load Customer Projects & Check For new Customer
        If Not Intersect(Target, Range("E4")) Is Nothing And Range("E4").Value <> Empty Then
            If Range("B5").Value = Empty Then 'New Customer
                If MsgBox(Range("E4").Value & " is not in your customer list. Would you like to add this customer?", vbYesNo, "New Customer") = vbNo Then
                  Range("E4:F4").ClearContents
                  Exit Sub
                End If
                CustRow = Customers.Range("A9999").End(xlUp).Row + 1  'First Avail Customer Row
                Customers.Range("A" & CustRow).Value = Range("B7").Value 'Next Customer ID
                Customers.Range("B" & CustRow).Value = Range("E4").Value 'Customer Name
            End If
            With Projects
                ProjRow = .Range("A99999").End(xlUp).Row
                If ProjRow < 4 Then Exit Sub
                .Range("B3:C" & ProjRow).AdvancedFilter xlFilterCopy, CriteriaRange:=.Range("K2:K3"), CopyToRange:=.Range("M2"), Unique:=True
            End With
        End If

'On Invoice Customer Change, Load Customer Address & Billing Items
        If Not Intersect(Target, Range("K4")) Is Nothing Then
            If Range("B19").Value <> "" Then
                CustRow = Range("B19").Value 'Customer Row
                Range("K5").Value = Customers.Range("C" & CustRow).Value 'Address 1
                Range("K6").Value = Customers.Range("D" & CustRow).Value & ", " & Customers.Range("E" & CustRow).Value & ", " & Customers.Range("F" & CustRow).Value
            Else 'Clear Address Fields
                Range("K5,K6").ClearContents
            End If
            BillingEntry_LoadList 'Run Macro to reload list
        End If
'On Invoice Item Change, but not on Invoice Load or Item Load
    If Not Intersect(Target, Range("K9:K35")) Is Nothing And Range("B24").Value = False And Range("B25").Value = False Then
        If Range("K" & Target.Row).Value <> Empty Then
                On Error Resume Next
                ServItemDBRow = ServItems.Range("ServItem_Name").Find(Target.Value, , xlValues, xlWhole).Row
                On Error GoTo 0
                If ServItemDBRow = 0 Then
                    MsgBox "Service Item Not Found"
                    Exit Sub
                End If
                Range("L" & Target.Row).Value = ServItems.Range("C" & ServItemDBRow).Value 'Description
                Range("N" & Target.Row).Value = ServItems.Range("D" & ServItemDBRow).Value 'Hourly Rate
        Else 'Clear Line Out
                If Target.Row > 8 Then Range("L" & Target.Row & ":N" & Target.Row).ClearContents 'Clear Item On Delete
        End If
    End If

'On Invoice Search Change
If Not Intersect(Target, Range("R2")) Is Nothing And Range("R2").Value <> Empty Then
    If Range("B22").Value = Empty Then
        MsgBox "Please enter a correct invoice #"
        Exit Sub
    End If
    Range("N3").Value = Range("R2").Value 'Set Invoice #
    Range("R2").ClearContents
    Invoice_Load 'Load Invoice
End If

'On Change of Billing Service Item, but not on Billing Item Load
If Not Intersect(Target, Range("E6")) Is Nothing And Range("B23").Value = False And Range("B12").Value <> "" Then
    ServItemDBRow = Range("B12").Value ' Service Item DB Row
    Range("E7").Value = ServItems.Range("C" & ServItemDBRow).Value 'Set Default Description
    Range("H6").Value = ServItems.Range("D" & ServItemDBRow).Value 'Set Default Rate
End If
End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)
If Target.CountLarge > 1 Then Exit Sub
If Shapes("AddItemBtn").Visible = True Then Shapes("AddItemBtn").Visible = msoFalse 'Hide Add Item Button if visible
'On Selection Of Billing item
If Not Intersect(Target, Range("D12:H9999")) Is Nothing And Range("C" & Target.Row).Value <> Empty Then
        Range("B2").Value = Range("C" & Target.Row).Value 'Set Billing ID
        Range("B17").Value = Target.Row 'Set Selected Row
        BillingEntry_Load 'Run Macro To load Item
        With Shapes("AddItemBtn")
                .Left = Range("I" & Target.Row).Left
                .Top = Range("I" & Target.Row).Top
                .Visible = msoCTrue
        End With
End If
End Sub
