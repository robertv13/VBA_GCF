Option Explicit

Private Sub Worksheet_Activate()

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshStatsHeuresPivotTables:Worksheet_Activate", 0)

    If fromMenu = False Then
        Debug.Print "wshFAC_Brouillon_Worksheet_Activate - Je ne viens pas du menu"
        Exit Sub
    End If

    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    Dim ws As Worksheet: Set ws = wshStatsHeuresPivotTables
    
    ws.Unprotect
    
    'Masquer toutes les colonnes, pour la confidentialité
    ws.Range("D:V").EntireColumn.Hidden = True
    
    Call TEC_Import_All
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100
    
    Application.Calculation = xlCalculationAutomatic
    
    'Ensure all pending events are processed - 2024-10-12 @ 11:33
    DoEvents
    
    Me.Range("B4").value = ""
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    'Batir la liste des options valides en fonction de l'utilisateur
    Dim listeChoixValides As String
    Select Case Fn_Get_Windows_Username
        Case "Guillaume", "GuillaumeCharron", "Robert M. Vigneault", "robertmv"
            listeChoixValides = "Tous,Guillaume,Vladimir,Michel,Annie"
        Case "vgervais"
            listeChoixValides = "Vladimir"
        Case "User"
            listeChoixValides = "Michel"
        Case "Annie"
            listeChoixValides = "Annie"
        Case Else
            listeChoixValides = ""
    End Select
    
    Dim plageValide As Range
    Set plageValide = ws.Range("B4")
    
    'Supprimer Validation de données existante
    plageValide.Validation.Delete
    
    'Ajouter la nouvelle validation avec la liste spécifiée
    plageValide.Validation.Add Type:=xlValidateList, AlertStyle:=xlValidAlertStop, _
                               Operator:=xlBetween, Formula1:=listeChoixValides

    'Activer la flèche déroulante pour la sélection
    plageValide.Validation.InCellDropdown = True
    
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    fromMenu = False

    'Libérer la mémoire
    Set plageValide = Nothing
    Set ws = Nothing
    
    Call Log_Record("wshStatsHeuresPivotTables:Worksheet_Activate", startTime)

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    
    Dim ws As Worksheet: Set ws = wshStatsHeuresPivotTables
    
    If Not Intersect(Target, ws.Range("B4")) Is Nothing Then
        ws.Unprotect
        
        'Changer la valeur sur la feuille TEC_TDB_Data pour les Advanced Filter
        Select Case Target.value
            Case "Guillaume"
                wshTEC_TDB_Data.Range("S6").value = "GC"
            Case "Vladimir"
                wshTEC_TDB_Data.Range("S6").value = "VG"
            Case "Michel"
                wshTEC_TDB_Data.Range("S6").value = "ML"
            Case "Annie"
                wshTEC_TDB_Data.Range("S6").value = "AR"
            Case "Tous"
                wshTEC_TDB_Data.Range("S6").value = ""
        End Select
        
        If wshTEC_TDB_Data.Range("BJ2").value <> "" Then
            ws.Range("S:V").EntireColumn.Hidden = False
        End If
    
        If wshTEC_TDB_Data.Range("AW2").value <> "" Then
            ws.Range("N:R").EntireColumn.Hidden = False
        End If
    
        If wshTEC_TDB_Data.Range("AJ2").value <> "" Then
            ws.Range("I:M").EntireColumn.Hidden = False
        End If
    
        If wshTEC_TDB_Data.Range("W2").value <> "" Then
            ws.Range("D:H").EntireColumn.Hidden = False
        End If
    
    End If
    
    Set ws = Nothing

End Sub

