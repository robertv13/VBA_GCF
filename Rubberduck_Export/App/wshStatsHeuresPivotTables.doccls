'@Folder("TEC - Pivot tables")

Option Explicit

Private Sub Worksheet_Activate()

    Dim startTime As Double: startTime = Timer
    Call modDev_Utils.EnregistrerLogApplication("wshStatsHeuresPivotTables:Worksheet_Activate", _
                                                                    vbNullString, 0)

    If gFromMenu = False Then
        Exit Sub
    End If

    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    Dim ws As Worksheet: Set ws = wshStatsHeuresPivotTables
    
    ws.Unprotect
    
    'Masquer toutes les colonnes, pour la confidentialité
    ws.Range("D:V").EntireColumn.Hidden = True
    
    'Ajuste les entêtes de colonnes
    ws.Range("D2").Value = "Données du " & wsdADMIN.Range("DateDebutSemaine") & " au " & wsdADMIN.Range("DateFinSemaine")
    ws.Range("I2").Value = "Données du " & wsdADMIN.Range("MoisDe") & " au " & wsdADMIN.Range("MoisA")
    ws.Range("N2").Value = "Données du " & wsdADMIN.Range("TrimDe") & " au " & wsdADMIN.Range("TrimA")
    ws.Range("S2").Value = "Données du " & wsdADMIN.Range("AnneeDe") & " au " & wsdADMIN.Range("AnneeA")
    
    Call modImport.ImporterTEC
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100
    
    DoEvents
    
    Me.Range("B4").Value = vbNullString
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    Application.Calculation = xlCalculationAutomatic
    
    'Batir la liste des options valides en fonction de l'utilisateur
    Dim listeChoixValides As String
    Select Case modFunctions.Fn_UtilisateurWindows()
        Case "Guillaume", "GuillaumeCharron", "gchar", "RobertMV", "robertmv"
            listeChoixValides = "Tous,Guillaume,Vladimir,Olivier,Michel,Marie-France,Annie"
        Case "vgervais", "Vlad_Portable"
            listeChoixValides = "Vladimir"
        Case "User"
            listeChoixValides = "Michel"
        Case "Oli_Portable"
            listeChoixValides = "Olivier"
        Case "MARIE-FRANCE"
            listeChoixValides = "Marie-France"
        Case "Annie"
            listeChoixValides = "Annie"
        Case Else
            listeChoixValides = vbNullString
    End Select
    
    Dim plageValide As Range
    Set plageValide = ws.Range("B4")
    
    'Supprimer Validation de données existante
    plageValide.Validation.Delete
    
    'Ajouter la nouvelle validation avec la liste spécifiée
    plageValide.Validation.Add Type:=xlValidateList, AlertStyle:=xlValidAlertStop, _
                               Operator:=xlBetween, Formula1:=listeChoixValides

    'Activer la flèche déroulante pour la sélection
    plageValide.Validation.InCellDropdown = True
    
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    gFromMenu = False

    'Libérer la mémoire
    Set plageValide = Nothing
    Set ws = Nothing
    
    Call modDev_Utils.EnregistrerLogApplication("wshStatsHeuresPivotTables:Worksheet_Activate", vbNullString, startTime)

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    
    Dim startTime As Double: startTime = Timer
    Call modDev_Utils.EnregistrerLogApplication("wshStatsHeuresPivotTables:Worksheet_Change", _
                                                              Target.Address, 0)
    
    Dim ws As Worksheet: Set ws = wshStatsHeuresPivotTables
    
    If Not Intersect(Target, ws.Range("B4")) Is Nothing Then
        ws.Unprotect
        
        'Changer la valeur sur la feuille TEC_TDB_Data pour les Advanced Filter
        Dim initProf As String
        Select Case Target.Value
            Case "Guillaume"
                initProf = "GC"
            Case "Vladimir"
                initProf = "VG"
            Case "Michel"
                initProf = "ML"
            Case "Olivier"
                initProf = "OB"
            Case "Marie-France"
                initProf = "MFP"
            Case "Annie"
                initProf = "AR"
            Case "Tous"
                initProf = vbNullString
            Case Else
                initProf = vbNullString
        End Select
        
        wshTEC_TDB_Data.Range("S7").Value = initProf
        
        DoEvents
        
        Dim zones As Variant
        zones = Array( _
            Array("BJ2", "S:V"), Array("AW2", "N:R"), Array("AJ2", "I:M"), Array("W2", "D:H"))
        
        Dim i As Long
        For i = LBound(zones) To UBound(zones)
            ws.Range(zones(i)(1)).EntireColumn.Hidden = (Trim(wshTEC_TDB_Data.Range(zones(i)(0)).Value) = "")
        Next i
        
        Application.ScreenUpdating = True
    
    End If
    
    'Libérer la mémoire
    Set ws = Nothing

    Call modDev_Utils.EnregistrerLogApplication("wshStatsHeuresPivotTables:Worksheet_Change", vbNullString, startTime)

End Sub

