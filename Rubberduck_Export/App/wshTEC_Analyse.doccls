'@Folder("Facturation")

Option Explicit

Private Sub Worksheet_Activate() '2024-06-13 @ 18:07

    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshTEC_Analyse:Worksheet_Activate", vbNullString, 0)

    On Error GoTo CleanUp

    Application.ScreenUpdating = False
    Application.EnableEvents = False

    Dim ws As Worksheet: Set ws = wshTEC_Analyse
'    ws.Activate

    'Import transactions from MASTER file
    Call modImport.ImporterClients
    Call modImport.ImporterTEC
    Call modImport.ImporterFacProjetsDetails
    Call modImport.ImporterFacProjetsEntete

    'Set the zoom factor to 100% when this worksheet is activated

    Application.Calculation = xlCalculationManual

   'Mettre la date du jour comme date butoir, AVANT d'effectuer un premier traitement
    Application.EnableEvents = True
    ws.Range("H3").NumberFormat = wsdADMIN.Range("B1").Value
    ws.Range("H3").Value = Date
    Application.EnableEvents = False

    Call modDev_Utils.DeterminerOrdreDeTabulation(ws)

    'Indique la dernière ligne explosé, qui doit être refermé en passant au suivant
    ws.Range("B2").Value = 0

    With ws
        .Unprotect
        gPreviousCellAddress = .Range("H3").Address
    End With

    'Fige le volet à la ligne 8 (première ligne de détail) - 2025-07-09 @ 06:54
    With ws
        .Visible = xlSheetVisible
        .Activate
        ActiveWindow.Zoom = 100
        ActiveWindow.FreezePanes = False
        Application.GoTo .Range("A8"), Scroll:=False
        .Range("A8").Select
        ActiveWindow.FreezePanes = True
    End With

    ws.Select
    
    'Libérer la mémoire
    Set ws = Nothing

    Call modDev_Utils.EnregistrerLogApplication("wshTEC_Analyse:Worksheet_Activate", vbNullString, startTime)

CleanUp:
    Application.Calculation = xlCalculationAutomatic
    Application.EnableEvents = True
    Application.ScreenUpdating = True

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshTEC_Analyse:Worksheet_SelectionChange", Target.Address, 0)
    
    If gPreviousCellAddress <> vbNullString Then
        Range(gPreviousCellAddress).Interior.Color = gCOULEUR_BASE_TEC
    End If
    
    'Is date been selected ?
    If Not Intersect(Target, wshTEC_Analyse.Range("H3")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
    End If
    
    'Check if the selected cell is in column C, D or H
    If Not Intersect(Target, wshTEC_Analyse.Columns("C")) Is Nothing Or _
        Not Intersect(Target, wshTEC_Analyse.Columns("D")) Is Nothing Or _
        Not Intersect(Target, wshTEC_Analyse.Columns("H")) Is Nothing Then
        
        Dim rowRange As Range
        Dim firstExpandedRow As Long
        If Target.Cells.count = 1 And Target.row > 6 Then
            If Target.Value <> vbNullString Then
                Call EffacerCheckBox
                If wshTEC_Analyse.Range("B2").Value <> 0 Then
                    firstExpandedRow = wshTEC_Analyse.Range("B2").Value
                    If Rows(firstExpandedRow).ShowDetail Then
                        'Referme le groupe sous cette ligne
                        Rows(firstExpandedRow).ShowDetail = False
                    End If
                End If
                'Loop through each row in the target selection
                Dim cell As Range
                For Each cell In Target
                    'Define the range for the entire row
                    Application.EnableEvents = False
                    wshTEC_Analyse.Range("B2").Value = Target.row
                    Application.EnableEvents = True
                    Set rowRange = cell.EntireRow
                    'Check if the row is part of a group
                    If rowRange.OutlineLevel > 1 Then
                        'Expand the group
                        If rowRange.ShowDetail = False Then
                            rowRange.ShowDetail = True
                            firstExpandedRow = Target.row
                        End If
                    End If
                Next cell
                If Me.Cells(Target.row, 2) = vbNullString Then
                    Call modTEC_Analyse.ConstruireSommaireHeures(Target.row)
                End If
            End If
        End If
    End If
    
    'Libérer la mémoire
    Set cell = Nothing
    Set rowRange = Nothing
    
    Call modDev_Utils.EnregistrerLogApplication("wshTEC_Analyse:Worksheet_SelectionChange", vbNullString, startTime)
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshTEC_Analyse:Worksheet_Change", Target.Address, 0)
    
    Dim fullDate As Variant
    
    'CutOffDate has changed
    If Not Intersect(Target, Range("H3")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_CompleteLaDate(Target.text, 45, 0)
        If fullDate <> "Invalid Date" Then
            Target.Value = fullDate
            Call TrierEtAjouterSousTotauxClient
        Else
            Call modDataValidation.AfficherMessageDateInvalide("wshTEC_Analyse_131")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.GoTo Range(Target.Address)
        End If
       
        'Future date ?
        If CDate(Range("H3").Value) > Date Then
            If MsgBox("Il n'est pas permis d'utiliser une date dans le futur !", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Application.EnableEvents = False
                Target.ClearContents
                Application.EnableEvents = True
                Application.GoTo Range(Target.Address)
            End If
        End If

'        Set rng = wshTEC_Analyse.Range("O9")
        GoTo exitSub
    End If

exitSub:

    Application.EnableEvents = True
    
    Call modDev_Utils.EnregistrerLogApplication("wshTEC_Analyse:Worksheet_Change", vbNullString, startTime)
  
End Sub

Sub CheckBox1_Click() '2024-07-18 @ 18:53

    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshTEC_Analyse:shpCheckBox_Click", vbNullString, 0)
    
    Dim ws As Worksheet: Set ws = wshTEC_Analyse
    
    'Reference your checkbox by name
    Dim chkBox As OLEObject
    Set chkBox = ws.OLEObjects("CheckBox1")
    
    'Get the address of the checkbox
    Dim chkBoxPosition As String
    chkBoxPosition = Fn_GetCheckBoxPosition(chkBox)
    
    'Get the row number of the checkBox
    Dim chkboxRow As Long
    chkboxRow = CLng(Replace(chkBoxPosition, "$N$", vbNullString))

    'Getting to the Client's Total Row (which is the line before the start of the summary)
    Dim i As Long
    For i = chkboxRow - 1 To 7 Step -1
        If ws.Range("M" & i).Value = vbNullString Then
            Exit For
        End If
    Next i
    Dim totalRow As Long
    totalRow = i
    
    'Additional code based on the checkbox state
    If chkBox.Object.Value = True Then
        
        'Is there a billing project that already exist for this customer ?
        Dim cell As String
        cell = Fn_DetruireLigneSiValeurEstTrouvee(ws.Range("C" & totalRow + 1).Value, _
                                                        ws.Range("D" & totalRow).Value)
        
        Select Case cell
            Case "REMPLACER"
                Application.EnableEvents = False
                ws.Range("D" & totalRow).Value = ws.Range("M" & chkboxRow).Value
                Application.EnableEvents = True
                
                Dim firstRow As Long: firstRow = totalRow + 1
                Dim lastRow As Long, r As Long
                r = firstRow
                Do While ws.Range("A" & r).Value <> vbNullString
                    lastRow = r
                    r = r + 1
                Loop
                
                Dim nomClient As String
                Dim clientID As String
                nomClient = ws.Range("C" & firstRow).Value
                clientID = Fn_ClientIDAPartirDuNomDeClient(nomClient)
                
                Dim projetID As Long 'ProjetID is establised by the next procedure
                Call AjouterProjetsDetailsBDMaster(clientID, firstRow, lastRow, projetID)
                Call AjouterProjetsDetailsBDLocale(clientID, firstRow, lastRow, projetID)
                
                r = firstRow
                Do While ws.Range("J" & r).Value <> vbNullString
                    r = r + 1
                Loop
                r = r - 1 'Last line of summary, excluding totals
                
                Dim arr() As Variant
                ReDim arr(1 To (r - firstRow + 1), 1 To 4)
                Dim rngSummary As Range: Set rngSummary = ws.Range("J" & firstRow & ":M" & r)
                arr = rngSummary.Value
                
                'Set the date
                Dim dateProjet As String
                dateProjet = Format$(ws.Range("H3").Value, "dd/mm/yyyy")
                
                'Determine the summary total
                Dim hono As Double
                hono = ws.Range("M" & r + 1).Value
                
                Call AjouterProjetsEnteteBDMaster(projetID, nomClient, clientID, dateProjet, hono, arr)
                Call AjouterProjetsEnteteBDLocale(projetID, nomClient, clientID, dateProjet, hono, arr)
                
                Call OrganiserEtFermerSousTotauxClient(firstRow)
                
                Application.EnableEvents = True
                
            Case "SUPPRIMER"
                Application.EnableEvents = False
                ws.Range("D" & totalRow).Value = 0
                Application.EnableEvents = True
                firstRow = totalRow + 1
                Call OrganiserEtFermerSousTotauxClient(firstRow)
                
            Case "RIEN_CHANGER"
                firstRow = totalRow + 1
                Call OrganiserEtFermerSousTotauxClient(firstRow)
            
        End Select
    Else
        ws.Range("D" & totalRow).Value = 0
        'Perform actions when checkbox is unchecked
        Application.EnableEvents = True
        Call OrganiserEtFermerSousTotauxClient(firstRow)

    End If
    
    Call modTEC_Analyse.EffacerSectionHonorairesEtCheckBox
    
    Application.EnableEvents = True
    
    'Libérer la mémoire
    Set chkBox = Nothing
    Set rngSummary = Nothing
    Set ws = Nothing
    
    Call modDev_Utils.EnregistrerLogApplication("wshTEC_Analyse:shpCheckBox_Click", vbNullString, startTime)

End Sub