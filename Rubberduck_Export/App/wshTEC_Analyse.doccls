'@Folder(GC_FISCALITÉ.1TEC)

Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate() '2024-06-13 @ 18:07

    Dim timerStart As Double: timerStart = Timer: Call Start_Timer("wshTEC_Analyse:Worksheet_Activate()")
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call TEC_Import_All
    Call FAC_Projets_Détails_Import_All
    Call FAC_Projets_Entête_Import_All
    
    'Set the zoom factor to 95% when this worksheet is activated
    ActiveWindow.Zoom = 95

    Me.Application.Calculation = xlCalculationAutomatic

    Dim ws As Worksheet: Set ws = wshTEC_Analyse
    
    Call TEC_Sort_Group_And_Subtotal
    
    Call SetTabOrder(ws)
    
    With ws
        .Unprotect
        previousCellAddress = .Range("I3").Address
    End With
    
    ws.Outline.ShowLevels RowLevels:=2
    
'    ActiveWindow.Panes(2).Activate
    Application.EnableEvents = False
    ws.Select
    ws.Range("I3").value = Format$(Now(), "mm/dd/yyyy")
    ws.Range("I3").Select
    Application.EnableEvents = True
    
    'Cleaning memory - 2024-07-11 @ 10:14
    Set ws = Nothing
    
    Call End_Timer("wshTEC_Analyse:Worksheet_Activate()", timerStart)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    Dim timerStart As Double: timerStart = Timer: Call Start_Timer("wshTEC_Analyse:Worksheet_SelectionChange()")
    
    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = BASIC_COLOR
    End If
    
    'Is date been selected ?
    If Not Intersect(Target, wshTEC_Analyse.Range("I3")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Check if the selected cell is in column C or H
    If Not Intersect(Target, wshTEC_Analyse.columns("C")) Is Nothing Or _
        Not Intersect(Target, wshTEC_Analyse.columns("H")) Is Nothing Then
        
        If Target.Cells.count = 1 And Target.row > 6 Then
            If Target.value <> "" Then
                Call Delete_CheckBox
                'Loop through each row in the target selection
                Dim cell As Range
                Dim rowRange As Range
                For Each cell In Target
                    'Define the range for the entire row
                    Set rowRange = cell.EntireRow
                    'Check if the row is part of a group
                    If rowRange.OutlineLevel > 1 Then
                        'Expand the group
                        If rowRange.ShowDetail = False Then
                            rowRange.ShowDetail = True
                        End If
                    End If
                Next cell
                If Me.Cells(Target.row, 2) = "" Then
                    Call Build_Hours_Summary(Target.row)
                End If
            End If
        End If
    End If
    
    Call End_Timer("wshTEC_Analyse:Worksheet_SelectionChange()", timerStart)
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim timerStart As Double: timerStart = Timer: Call Start_Timer("wshTEC_Analyse:Worksheet_Change()")
    
    Dim fullDate As Variant
    
    'CutOffDate has changed
    If Not Intersect(Target, Range("I3")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = CompleteDate(CStr(Target.text))
        If fullDate <> "Invalid Date" Then
            Target.value = fullDate
            Call TEC_Sort_Group_And_Subtotal

'            Application.GoTo wshTEC_Analyse.Range("O9")
        Else
            Call MsgBoxInvalidDate
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
       
        'Future date ?
        If CDate(Range("I3").value) > Format$(Now(), "dd/mm/yyyy") Then
            If MsgBox("Il n'est pas permis d'utiliser une date dans le futur !", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Application.EnableEvents = False
                Target.ClearContents
                Application.EnableEvents = True
                Application.Goto Range(Target.Address)
            End If
        End If

'        Set rng = wshTEC_Analyse.Range("O9")
        GoTo ExitSub
    End If

ExitSub:

    Application.EnableEvents = True
    
    'Cleaning memory - 2024-07-01 @ 09:34

    Call End_Timer("wshTEC_Analyse:Worksheet_Change()", timerStart)
  
End Sub

Private Sub CheckBox1_Click() '2024-07-18 @ 18:53

    Dim ws As Worksheet: Set ws = wshTEC_Analyse
    
    'Reference your checkbox by name
    Dim chkBox As OLEObject
    Set chkBox = ws.OLEObjects("CheckBox1")
    
    'Get the address of the checkbox
    Dim chkBoxPosition As String
    chkBoxPosition = GetCheckBoxPosition(chkBox)
    
    'Get the row number of the checkBox
    Dim chkboxRow As Long
    chkboxRow = CLng(Replace(chkBoxPosition, "$O$", ""))

    'Getting to the Total Row (which is the line before the start of the summary)
    Dim i As Long
    For i = chkboxRow To 7 Step -1
        If ws.Range("N" & i).value = "" Then
            Exit For
        End If
    Next i
    Dim totalRow As Long
    totalRow = i
    
    'Additional code based on the checkbox state
    If chkBox.Object.value = True Then
        
        'Is there a billing project that already exist for this customer ?
        Dim cell As String
        cell = Verify_And_Delete_Rows_If_Value_Is_Found(ws.Range("C" & totalRow + 1).value, _
                                                        ws.Range("D" & totalRow).value)
        
        Select Case cell
            Case "REMPLACER"
                ws.Range("D" & totalRow).value = ws.Range("N" & chkboxRow).value
                
                Dim firstRow As Long: firstRow = totalRow + 1
                Dim lastRow As Long, r As Long
                r = firstRow
                Do While ws.Range("A" & r).value <> ""
                    lastRow = r
                    r = r + 1
                Loop
                
                Dim nomClient As String
                Dim clientID As Long
                nomClient = ws.Range("C" & firstRow).value
                clientID = Fn_GetID_From_Client_Name(nomClient)
                
                Dim projetID As Long 'ProjetID is establised by the next procedure
                Call FAC_Projets_Détails_Add_Record_To_DB(clientID, firstRow, lastRow, projetID)
                Call FAC_Projets_Détails_Add_Record_Locally(clientID, firstRow, lastRow, projetID)
                
                r = firstRow
                Do While ws.Range("K" & r).value <> ""
                    r = r + 1
                Loop
                r = r - 1 'Last line of summary, excluding totals
                
                Dim arr() As Variant
                ReDim arr(1 To (r - firstRow + 1), 1 To 4)
                Dim rngSummary As Range: Set rngSummary = ws.Range("K" & firstRow & ":N" & r)
                arr = rngSummary.value
                
                'Set the date
                Dim dateProjet As String
                dateProjet = Format$(ws.Range("I3").value, "dd/mm/yyyy")
                
                'Determine the summary total
                Dim hono As Double
                hono = ws.Range("N" & r + 1).value
                
                Call FAC_Projets_Entête_Add_Record_To_DB(projetID, nomClient, clientID, dateProjet, hono, arr)
                Call FAC_Projets_Entête_Add_Record_Locally(projetID, nomClient, clientID, dateProjet, hono, arr)
                
            Case "SUPPRIMER"
                ws.Range("D" & totalRow).value = 0
'                firstRow = totalRow + 1
'                Call Groups_SubTotals_Collapse_A_Client(firstRow)
                
            Case "RIEN_CHANGER"
'                firstRow = totalRow + 1
'                Call Groups_SubTotals_Collapse_A_Client(firstRow)
            
        End Select
    Else
        ws.Range("D" & totalRow).value = 0
        'Perform actions when checkbox is unchecked
    End If
    
End Sub

Private Sub Get_CheckBox_Position(cb As OLEObject)

    'Set your worksheet (adjust this to match your worksheet name)
    Dim ws As Worksheet
    Set ws = wshTEC_Analyse
    
    'Reference your checkbox by name
    Dim checkBox As OLEObject
    Set checkBox = ws.OLEObjects(cb)
    
    'Get the cell that contains the top-left corner of the CheckBox
    Dim checkBoxCell As Range
    Set checkBoxCell = checkBox.TopLeftCell
    
    ' Display the address of the cell
    MsgBox "The CheckBox is located at cell: " & checkBoxCell.Address

End Sub

Sub Delete_CheckBox()

    'Set your worksheet (adjust this to match your worksheet name)
    Dim ws As Worksheet: Set ws = wshTEC_Analyse
    
    'Check if CheckBox1 exists and then delete it
    Dim checkBox As OLEObject
    Dim i As Long
    For i = 1 To 5
        On Error Resume Next
        Set checkBox = ws.OLEObjects("CheckBox" & i)
        If Not checkBox Is Nothing Then
            checkBox.delete
        End If
        On Error GoTo 0
    Next i
End Sub

Sub Back_To_TEC_Menu()

    wshTEC_Analyse.Visible = xlSheetHidden
    
    wshMenuTEC.Activate
    wshMenuTEC.Range("A1").Select

End Sub

