Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate() '2024-06-13 @ 18:07

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshTEC_Analyse:Worksheet_Activate", 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    Dim ws As Worksheet: Set ws = wshTEC_Analyse
    
    'Import transactions from MASTER file
    Call Client_List_Import_All
    Call TEC_Import_All
    Call FAC_Projets_Détails_Import_All
    Call FAC_Projets_Entête_Import_All
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100

    Me.Application.Calculation = xlCalculationAutomatic

    'Mettre la date du jour comme date butoir, AVANT d'effectuer un premier traitement
    Me.Range("H3").value = Format$(Now(), wshAdmin.Range("B1").value) '2024-10-29 @ 07:34
    
    Call TEC_Sort_Group_And_Subtotal
    
    Call SetTabOrder(ws)
    
    'Indique la dernière ligne explosé, qui doit être refermé en passant au suivant
    Application.EnableEvents = False
    ws.Range("B2").value = 0
    Application.EnableEvents = True
    
    With ws
        .Unprotect
        previousCellAddress = .Range("H3").Address
    End With
    
    ws.Outline.ShowLevels RowLevels:=2
    
    'Active le volet inférieur (Pane 2) et défile pour positionner la ligne 7 en haut de ce volet
    With ActiveWindow.Panes(2)
        .ScrollRow = 7
    End With

    Dim currentScrollRow As Long
    currentScrollRow = ActiveWindow.Panes(2).ScrollRow
    
    With ActiveWindow.Panes(1)
        Activate
    End With
    
    'Désactive les événements pour éviter tout problème lors de la mise à jour
    Application.EnableEvents = False
    
'    ws.Range("H3").value = Now()
'    ws.Range("H3").Select
    
    'Réapplique la position du volet inférieur pour maintenir l'affichage à la ligne 7
    ActiveWindow.Panes(2).ScrollRow = currentScrollRow
    
    'Reactive les événements
    Application.EnableEvents = True
    
    ws.Visible = xlSheetVisible
    
    'Libérer la mémoire
    Set ws = Nothing
    
    Call Log_Record("wshTEC_Analyse:Worksheet_Activate", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshTEC_Analyse:Worksheet_SelectionChange - " & Target.Address, 0)
    
    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = BASIC_COLOR
    End If
    
    'Is date been selected ?
    If Not Intersect(Target, wshTEC_Analyse.Range("H3")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Check if the selected cell is in column C, D or H
    If Not Intersect(Target, wshTEC_Analyse.columns("C")) Is Nothing Or _
        Not Intersect(Target, wshTEC_Analyse.columns("D")) Is Nothing Or _
        Not Intersect(Target, wshTEC_Analyse.columns("H")) Is Nothing Then
        
        Dim rowRange As Range
        Dim firstExpandedRow As Long
        If Target.Cells.count = 1 And Target.row > 6 Then
            If Target.value <> "" Then
                Call Delete_CheckBox
                If wshTEC_Analyse.Range("B2").value <> 0 Then
                    firstExpandedRow = wshTEC_Analyse.Range("B2").value
                    If rows(firstExpandedRow).ShowDetail Then
                        'Referme le groupe sous cette ligne
                        rows(firstExpandedRow).ShowDetail = False
                    End If
                End If
                'Loop through each row in the target selection
                Dim cell As Range
                For Each cell In Target
                    'Define the range for the entire row
                    Application.EnableEvents = False
                    wshTEC_Analyse.Range("B2").value = Target.row
                    Application.EnableEvents = True
                    Set rowRange = cell.EntireRow
                    'Check if the row is part of a group
                    If rowRange.OutlineLevel > 1 Then
                        'Expand the group
                        If rowRange.ShowDetail = False Then
                            rowRange.ShowDetail = True
                            firstExpandedRow = Target.row
                        End If
                    End If
                Next cell
                If Me.Cells(Target.row, 2) = "" Then
                    Call Build_Hours_Summary(Target.row)
                End If
            End If
        End If
    End If
    
    'Libérer la mémoire
    Set cell = Nothing
    Set rowRange = Nothing
    
    Call Log_Record("wshTEC_Analyse:Worksheet_SelectionChange", startTime)
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshTEC_Analyse:Worksheet_Change - " & Target.Address, 0)
    
    Dim fullDate As Variant
    
    'CutOffDate has changed
    If Not Intersect(Target, Range("H3")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(Target.Text, 45, 0)
        If fullDate <> "Invalid Date" Then
            Target.value = Format$(fullDate, wshAdmin.Range("B1").value)
            Call TEC_Sort_Group_And_Subtotal
        Else
            Call MsgBoxInvalidDate("wshTEC_Analyse_131")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.GoTo Range(Target.Address)
        End If
       
        'Future date ?
        If CDate(Range("H3").value) > Now() Then
            If MsgBox("Il n'est pas permis d'utiliser une date dans le futur !", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Application.EnableEvents = False
                Target.ClearContents
                Application.EnableEvents = True
                Application.GoTo Range(Target.Address)
            End If
        End If

'        Set rng = wshTEC_Analyse.Range("O9")
        GoTo ExitSub
    End If

ExitSub:

    Application.EnableEvents = True
    
    Call Log_Record("wshTEC_Analyse:Worksheet_Change", startTime)
  
End Sub

Private Sub CheckBox1_Click() '2024-07-18 @ 18:53

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshTEC_Analyse:CheckBox1_Click", 0)
    
    Dim ws As Worksheet: Set ws = wshTEC_Analyse
    
    'Reference your checkbox by name
    Dim chkBox As OLEObject
    Set chkBox = ws.OLEObjects("CheckBox1")
    
    'Get the address of the checkbox
    Dim chkBoxPosition As String
    chkBoxPosition = GetCheckBoxPosition(chkBox)
    
    'Get the row number of the checkBox
    Dim chkboxRow As Long
    chkboxRow = CLng(Replace(chkBoxPosition, "$N$", ""))

    'Getting to the Client's Total Row (which is the line before the start of the summary)
    Dim i As Long
    For i = chkboxRow To 7 Step -1
        If ws.Range("M" & i).value = 0 Then
            Exit For
        End If
    Next i
    Dim totalRow As Long
    totalRow = i
    
    'Additional code based on the checkbox state
    If chkBox.Object.value = True Then
        
        'Is there a billing project that already exist for this customer ?
        Dim cell As String
        cell = Verify_And_Delete_Rows_If_Value_Is_Found(ws.Range("C" & totalRow + 1).value, _
                                                        ws.Range("D" & totalRow).value)
        
        Select Case cell
            Case "REMPLACER"
                Application.EnableEvents = False
                ws.Range("D" & totalRow).value = ws.Range("M" & chkboxRow).value
                Application.EnableEvents = True
                
                Dim firstRow As Long: firstRow = totalRow + 1
                Dim lastRow As Long, r As Long
                r = firstRow
                Do While ws.Range("A" & r).value <> ""
                    lastRow = r
                    r = r + 1
                Loop
                
                Dim nomClient As String
                Dim clientID As String
                nomClient = ws.Range("C" & firstRow).value
                clientID = Fn_GetID_From_Client_Name(nomClient)
                
                Dim projetID As Long 'ProjetID is establised by the next procedure
                Call FAC_Projets_Détails_Add_Record_To_DB(clientID, firstRow, lastRow, projetID)
                Call FAC_Projets_Détails_Add_Record_Locally(clientID, firstRow, lastRow, projetID)
                
                r = firstRow
                Do While ws.Range("J" & r).value <> ""
                    r = r + 1
                Loop
                r = r - 1 'Last line of summary, excluding totals
                
                Dim arr() As Variant
                ReDim arr(1 To (r - firstRow + 1), 1 To 4)
                Dim rngSummary As Range: Set rngSummary = ws.Range("J" & firstRow & ":M" & r)
                arr = rngSummary.value
                
                'Set the date
                Dim dateProjet As String
                dateProjet = Format$(ws.Range("H3").value, "dd/mm/yyyy")
                
                'Determine the summary total
                Dim hono As Double
                hono = ws.Range("M" & r + 1).value
                
                Call FAC_Projets_Entête_Add_Record_To_DB(projetID, nomClient, clientID, dateProjet, hono, arr)
                Call FAC_Projets_Entête_Add_Record_Locally(projetID, nomClient, clientID, dateProjet, hono, arr)
                
                Call Groups_SubTotals_Collapse_A_Client(firstRow)
                
                Application.EnableEvents = True
                
            Case "SUPPRIMER"
                ws.Range("D" & totalRow).value = 0
                firstRow = totalRow + 1
                Call Groups_SubTotals_Collapse_A_Client(firstRow)
                
            Case "RIEN_CHANGER"
                firstRow = totalRow + 1
                Call Groups_SubTotals_Collapse_A_Client(firstRow)
            
        End Select
    Else
        ws.Range("D" & totalRow).value = 0
        'Perform actions when checkbox is unchecked
        Application.EnableEvents = True
        Call Groups_SubTotals_Collapse_A_Client(firstRow)

    End If
    
    Call Clear_Fees_Summary_And_CheckBox
    
    Application.EnableEvents = True
    
    'Libérer la mémoire
    Set chkBox = Nothing
    Set rngSummary = Nothing
    Set ws = Nothing
    
    Call Log_Record("wshTEC_Analyse:CheckBox1_Click", startTime)

End Sub

Private Sub Get_CheckBox_Position(cb As OLEObject)

    'Set your worksheet (adjust this to match your worksheet name)
    Dim ws As Worksheet
    Set ws = wshTEC_Analyse
    
    'Reference your checkbox by name
    Dim checkBox As OLEObject
    Set checkBox = ws.OLEObjects(cb)
    
    'Get the cell that contains the top-left corner of the CheckBox
    Dim checkBoxCell As Range
    Set checkBoxCell = checkBox.TopLeftCell
    
    ' Display the address of the cell
    MsgBox "The CheckBox is located at cell: " & checkBoxCell.Address
    
    'Libérer la mémoire
    Set checkBox = Nothing
    Set checkBoxCell = Nothing
    Set ws = Nothing

End Sub

Sub Delete_CheckBox()

    'Assigner la feuille à ws
    Dim ws As Worksheet: Set ws = wshTEC_Analyse
    
    'Si CheckBox* existe, l'effacer
    Dim checkBox As OLEObject
    Dim i As Long
    For i = 1 To 5
        On Error Resume Next
        Set checkBox = ws.OLEObjects("CheckBox" & i)
        If Not checkBox Is Nothing Then
            checkBox.Delete
            
        End If
        On Error GoTo 0
    Next i
    
    'Libérer la mémoire
    Set checkBox = Nothing
    Set ws = Nothing
    
End Sub

Sub Back_To_TEC_Menu()

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshTEC_Analyse:Back_To_TEC_Menu", 0)
    
    Call Clear_Fees_Summary_And_CheckBox
    Call Log_Record("     wshTEC_Analyse:Back_To_TEC_Menu - La zone Sommaire des Honoraires a été nettoyée", -1)
    
    wshTEC_Analyse.Visible = xlSheetHidden
    
    wshMenuTEC.Activate
    wshMenuTEC.Range("A1").Select
    
    Call Log_Record("wshTEC_Analyse:Back_To_TEC_Menu", startTime)

End Sub