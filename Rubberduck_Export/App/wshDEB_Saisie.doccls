'@Folder("Saisie_Déboursé")

Option Explicit

Private Sub Worksheet_Activate()

    If Not gFromMenu Then
        Exit Sub
    End If
    
    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshDEB_Saisie:Worksheet_Activate", vbNullString, 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Application.Calculation = xlCalculationAutomatic
    
    'Import transactions from MASTER file
    Call modImport.ImporterFournisseurs
    Call modImport.ImporterDebTrans
    Call modImport.ImporterDebRecurrent
    Call modImport.ImporterGLTransactions
    
    Dim ws As Worksheet: Set ws = wshDEB_Saisie
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100
    
    'S'assurer que les colonnes A & B sont masquées
    On Error Resume Next
    Me.Columns("A:B").Hidden = True
    On Error GoTo 0
    
    'Certaines cellules perdent la couleur du background
    Dim cellsToColor As Range
    Set cellsToColor = Union(ws.Range("E4"), ws.Range("I4"), ws.Range("N4"), ws.Range("F5"), _
                                ws.Range("G5"), ws.Range("J5"), ws.Range("E6"), _
                                ws.Range("L6"), ws.Range("N6"))
    Call modAppli_Utils.RemplirPlageAvecCouleur(cellsToColor, gCOULEUR_BASE_COMPTABILITE)
    
    Call modDev_Utils.DeterminerOrdreDeTabulation(ws)
    
    Call EffacerCellulesSaisieDEB
    
    'Rétablir le bouton "Mettre à jour / Renversement"
    Dim shp As Shape
    Set shp = ws.Shapes("shpMettreAJour")
    With shp
        .Left = 665
        .Width = 138
        .Height = 27
        .Fill.ForeColor.RGB = 12566463
        .TextFrame2.TextRange.text = "Mettre à jour"
        .TextFrame2.TextRange.Font.Fill.ForeColor.RGB = RGB(0, 0, 0) 'Noir
    End With
    
    Call SauvegarderParametresForme(shp)
    
    'Protect the Worksheet
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    gNumeroDebourseARenverser = -1
    Me.Range("B7").Value = False
    
    Me.Range("O4").Value = Format$(Date, wsdADMIN.Range("B1").Value)
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    Me.Range("F4").Select
    
    'Libérer la mémoire
    Set cellsToColor = Nothing
    Set shp = Nothing
    Set ws = Nothing
    
    Call modDev_Utils.EnregistrerLogApplication("wshDEB_Saisie:Worksheet_Activate", vbNullString, startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    'Remove the background color of the previous Cell
    If gPreviousCellAddress <> vbNullString Then
        Range(gPreviousCellAddress).Interior.Color = xlNone
    End If
    
    Dim ws As Worksheet: Set ws = wshDEB_Saisie
    
    'Save the current cell Address for next Selection change
    gPreviousCellAddress = Target.Address

    'Type is selected
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Beneficiaire is selected
    If Not Intersect(Target, Range("J4")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Date is selected
    If Not Intersect(Target, Range("O4")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Description is selected
    If Not Intersect(Target, Range("$F$6")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Reference is selected
    If Not Intersect(Target, Range("M6")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Total Amount is selected
    If Not Intersect(Target, Range("O6")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Account is selected
    If Not Intersect(Target, Range("F9:F23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Application.EnableEvents = False
        ws.Range("B4").Value = Target.Address
        Application.EnableEvents = True
        Exit Sub
    End If
    
    'Tax code is selected (TPS/TVQ is the default)
    If Not Intersect(Target, Range("H9:H23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Target.Value = "TPS/TVQ"
        Exit Sub
    End If
    
    'Amount is selected (Amount to distribute by default)
    If Not Intersect(Target, Range("I9:I23")) Is Nothing Then
        If Target.Value = vbNullString Then
            Application.EnableEvents = False
            Dim suggestedAmount As Double
            suggestedAmount = ws.Range("O6").Value - ws.Range("I26").Value
            If suggestedAmount > 0 Then
                Call DEBSuggestedAmount(Target.row, suggestedAmount)
            End If
            Application.EnableEvents = True
        End If
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
   
   'Force GST/TPS calculation with a formula, but also accept user input
    If Not Intersect(Target, Range("J9:J23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Force PST/TVQ calculation with a formula, but also accept user input
    If Not Intersect(Target, Range("K9:K23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Credit GST/TPS
    If Not Intersect(Target, Range("L9:L23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If
    
    'Credit PST/TVQ
    If Not Intersect(Target, Range("M9:M23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        Exit Sub
    End If

    'Calculate the NET amount (Expense), but also accept user input
    If Not Intersect(Target, Range("N9:O23")) Is Nothing Then
        If Range("I" & Target.row).Value <> 0 And _
            Range("H" & Target.row).Value <> "M" Then
                Range("N" & Target.row).formula = "=round(RC[-5]-RC[-2]-RC[-1],2)"
        End If
        Target.Interior.Color = gCOULEUR_SAISIE
         Exit Sub
    End If

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    
'    On Error GoTo ErrorHandler '@TODO
    
    Application.EnableEvents = False
    
    Call Process_Change(Target)
    
    Application.EnableEvents = True
    
    Exit Sub
    
ErrorHandler:
    Application.EnableEvents = True 'Ensure events are re-enabled if an error occurs
    MsgBox "Erreur " & Err.Number & ": " & Err.description, vbCritical, "Erreur # APP-002"

End Sub

Private Sub Process_Change(ByVal Target As Range)

    Dim ws As Worksheet: Set ws = wshDEB_Saisie
    
    Dim startCell As Range: Set startCell = ws.Range("E9")
    
    Dim GLRange As Range: Set GLRange = ws.Range(startCell.offset(0, 0), startCell.offset(14, 0))
    Dim amountRange As Range: Set amountRange = ws.Range(startCell.offset(0, 2), startCell.offset(14, 2))
    Dim gstRange As Range: Set gstRange = ws.Range(startCell.offset(0, 3), startCell.offset(14, 3))
    Dim pstRange As Range: Set pstRange = ws.Range(startCell.offset(0, 4), startCell.offset(14, 4))
    Dim gstCreditRange As Range: Set gstCreditRange = ws.Range(startCell.offset(0, 5), startCell.offset(14, 5))
    Dim pstCreditRange As Range: Set pstCreditRange = ws.Range(startCell.offset(0, 6), startCell.offset(14, 6))
    Dim netAmountRange As Range: Set netAmountRange = ws.Range(startCell.offset(0, 7), startCell.offset(14, 7))
    
    Dim fullDate As Variant
    Dim amount As Currency
    Dim gst As Currency, pst As Currency
    Dim gstCredit As Currency
    Dim pstCredit As Currency
    Dim netAmount As Currency
    
    'Type de déboursé a changé
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        If Target.Value = "AUTO" Then
            Call modDEB_Saisie.AfficherDeboursRecurrent
            Exit Sub
        End If
        If Target.Value = "Renversement" Then
            Call modDEB_Saisie.ConstruireEcritureDEBRenversement
        End If
    End If
    
    'Date has changed
    If Not Application.Intersect(Target, Range("O4")) Is Nothing Then
        'Temporarily disable events to prevent infinite loop
        Application.EnableEvents = False
        
        'Try to convert the input to a complete valid date
        fullDate = Fn_CompleteLaDate(Target.text, 30, 15)
        
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            Target.Value = Format$(fullDate, wsdADMIN.Range("B1").Value)
        Else
            Call modDataValidation.AfficherMessageDateInvalide("wshDEB_Saisie_190")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
        Application.EnableEvents = True
        Exit Sub
    End If
    
    'Account has changed
    If Not Intersect(Target, GLRange) Is Nothing Then
        If Target.Value <> vbNullString Then
            Application.EnableEvents = False
            wshDEB_Saisie.Range("Q" & Target.row).Value = _
                Fn_GetGL_Code_From_GL_Description(Target.Value)
            Application.EnableEvents = True
        End If
    End If
    
    'Total Amount has changed
    If Not Intersect(Target, amountRange) Is Nothing Then
        'If the user changes the Amount cell
        Dim cell As Range
        For Each cell In Target
            If cell.Value = 0 Then
                cell.offset(0, 1).Value = vbNullString 'Clear GST
                cell.offset(0, 2).Value = vbNullString 'Clear PST
                cell.offset(0, 3).Value = vbNullString 'Clear GST_Credit
                cell.offset(0, 4).Value = vbNullString 'Clear PST_Credit
                cell.offset(0, 5).Value = vbNullString 'Clear netAmount
                'Move focus to netAmount
                If gPreviousCellAddress <> vbNullString Then
                    Range(gPreviousCellAddress).Interior.Color = xlNone
                End If
                cell.offset(0, 5).Select 'Input NET amount
            Else
'                If wshDEB_Saisie.Range("H" & target.row).Value <> "M" Then
                    Call modDEB_Saisie.CalculerTaxesEtIntrants(wshDEB_Saisie.Range("O4").Value, _
                                                       wshDEB_Saisie.Range("H" & Target.row).Value, _
                                                       Target.Value, _
                                                       gst, pst, gstCredit, pstCredit, _
                                                       netAmount)

                    Application.EnableEvents = False
                    cell.offset(0, 1).Value = gst
                    cell.offset(0, 2).Value = pst
                    cell.offset(0, 3).Value = gstCredit
                    cell.offset(0, 4).Value = pstCredit
                    cell.offset(0, 5).Value = netAmount
                    Application.EnableEvents = True
                    If gPreviousCellAddress <> vbNullString Then
                        Range(gPreviousCellAddress).Interior.Color = xlNone
                    End If
                    'Move to the next line
                    cell.offset(1, -4).Select
'                End If
            End If
        Next cell
    End If
    
    If Not Intersect(Target, netAmountRange) Is Nothing Then
        'If the user changes the netAmount cell
        Dim cellNet As Range
        For Each cellNet In Target
            Debug.Print "#003 - " & cellNet.Address & " = " & cellNet.Value
            If cellNet.Value <> vbNullString Then
                amount = 0
                Debug.Print "#004 - Net amount (Manual Input) netAmount - " & wshDEB_Saisie.Range("O4").Value & " " & wshDEB_Saisie.Range("H" & Target.row).Value & " " & _
                    amount & " " & gst & " " & pst & " " & gstCredit & " " & pstCredit & " " & _
                    Target.Value
                'Calculate GST, PST, GST_Credit, PST_Credit based on the Amount
                If wshDEB_Saisie.Range("H" & Target.row).Value <> "M" Then
                    Call modDEB_Saisie.CalculerTaxesEtIntrants(wshDEB_Saisie.Range("O4").Value, _
                                                       wshDEB_Saisie.Range("H" & Target.row).Value, _
                                                       amount, _
                                                       gst, pst, gstCredit, pstCredit, _
                                                       Target.Value)

                    'Calculate Amount, GST, PST, GST_Credit, PST_Credit based on the netAmount
                    Application.EnableEvents = False
                    Debug.Print "#005 - " & cellNet.Address
                    cellNet.offset(0, -5).Value = amount
                    cellNet.offset(0, -4).Value = gst
                    cellNet.offset(0, -3).Value = pst
                    cellNet.offset(0, -2).Value = gstCredit
                    cellNet.offset(0, -1).Value = pstCredit
                    Application.EnableEvents = True
                    'Move to the previous cell
                    cellNet.offset(0, -1).Select
                End If
            End If
        Next cellNet
    End If
    
    If Not Intersect(Target, gstRange) Is Nothing Then
        gst = cell.offset(0, 1).Value
    End If

    If Not Intersect(Target, pstRange) Is Nothing Then
        pst = cell.offset(0, 2).Value
    End If
    
    If Not Intersect(Target, gstCreditRange) Is Nothing Then
        gstCredit = cell.offset(0, 3).Value
        netAmount = amount - gstCredit
        cell.offset(0, 5).Value = netAmount
    End If
    
    If Not Intersect(Target, pstCreditRange) Is Nothing Then
        pstCredit = cell.offset(0, 4).Value
        netAmount = amount - gstCredit - pstCredit
        cell.offset(0, 5).Value = netAmount
    End If
    
    'Libérer la mémoire
    Set amountRange = Nothing
    Set cell = Nothing
    Set cellNet = Nothing
    Set GLRange = Nothing
    Set gstRange = Nothing
    Set gstCreditRange = Nothing
    Set netAmountRange = Nothing
    Set pstRange = Nothing
    Set pstCreditRange = Nothing
    Set startCell = Nothing
    Set ws = Nothing
    
End Sub

Sub DEBSuggestedAmount(rowNumber As Long, suggestedAmount As Double)

    Dim ws As Worksheet: Set ws = wshDEB_Saisie
    
    Application.EnableEvents = False
    
    Dim targetCell As Range: Set targetCell = ws.Cells(rowNumber, 5 + 4) ' Assuming E9 is the starting cell
    
    targetCell.Value = suggestedAmount
    
    Application.EnableEvents = True
    
    Call Process_Change(targetCell)
    
    'Libérer la mémoire
    Set targetCell = Nothing
    Set ws = Nothing
    
End Sub

