Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate()

    Dim timerStart As Double: timerStart = Timer: Call Start_Routine("wshDEB_Saisie:Worksheet_Activate()")
    
    Application.ScreenUpdating = False
    
    Dim ws As Worksheet
    Set ws = ThisWorkbook.Sheets("DEB_Saisie")
    
    'Import all transactions from Master
    Call GL_Trans_Import_All
    
    'Set the zoom factor to 95% when this worksheet is activated
    ActiveWindow.Zoom = 95
    
    Application.EnableEvents = False
    
    'Hide column A & B
    With ws
        .Unprotect
        .Range("A:B").EntireColumn.Hidden = True
        .EnableCalculation = True
        .Protect UserInterfaceOnly:=True
    End With
    
    Call DEB_Saisie_Clear_All_Cells

    Application.ScreenUpdating = True

    Call Output_Timer_Results("wshDEB_Saisie:Worksheet_Activate()", timerStart)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = xlNone
    End If
    
    'Type is selected
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        Target.Interior.Color = vbYellow
    End If
    
    'Date is selected
    If Not Intersect(Target, Range("O4")) Is Nothing Then
        Target.Interior.Color = vbYellow
    End If
    
    'Description is selected
    If Not Intersect(Target, Range("$F$6:$K$6")) Is Nothing Then
        Target.Interior.Color = vbYellow
    End If
    
    'Reference is selected
    If Not Intersect(Target, Range("M6")) Is Nothing Then
        Target.Interior.Color = vbYellow
    End If
    
    'Total Amount is selected
    If Not Intersect(Target, Range("O6")) Is Nothing Then
        Target.Interior.Color = vbYellow
    End If
    
    'Account is selected
    If Not Intersect(Target, Range("F9:F23")) Is Nothing Then
        Target.Interior.Color = vbYellow
    End If
    
    'Tax code is selected (FP is the default)
    If Not Intersect(Target, Range("H9:H23")) Is Nothing Then
        Application.EnableEvents = False
        Target.value = "FP"
        Application.EnableEvents = True
    End If
    
     'Amount is selected (Amount to distribute by default)
    If Not Intersect(Target, Range("I9:I23")) Is Nothing Then
        If Target.value = "" Then
            Application.EnableEvents = False
            Target.value = wshDEB_Saisie.Range("O6").value - wshDEB_Saisie.Range("I26").value
            Target.Interior.Color = vbYellow
            Application.EnableEvents = True
        End If
    End If
   
   'Force GST/TPS calculation with a formula, but also accept user input
    If Not Intersect(Target, Range("J9:J23")) Is Nothing Then
        If Range("I" & Target.row).value <> 0 Then
            If InStr("FP^REP^", Range("H" & Target.row) & "^") = 1 Or _
                InStr("FP^REP^", Range("H" & Target.row) & "^") = 4 Then
                    Range("J" & Target.row).formula = "=round((RC[-1]/1.14975)*0.05,2)"
            ElseIf InStr("F^", Range("H" & Target.row) & "^") = 1 Then
                Range("J" & Target.row).formula = "=round((RC[-1]/1.05)*0.05,2)"
            Else
                Range("J" & Target.row).value = "0"
            End If
        End If
    End If
    
    'Force PST/TVQ calculation with a formula, but also accept user input
    If Not Intersect(Target, Range("K9:K23")) Is Nothing Then
        If Range("I" & Target.row).value <> 0 Then
            If InStr("FP^REP^", Range("H" & Target.row) & "^") = 1 Or _
                InStr("FP^REP^", Range("H" & Target.row) & "^") = 4 Then
                Range("K" & Target.row).formula = "=round((RC[-2]/1.14975)*0.09975,2)"
            ElseIf InStr("P^", Range("H" & Target.row) & "^") = 1 Then
                Range("K" & Target.row).formula = "=round((RC[-2]/1.09975)*0.09975,2)"
            Else
                Range("K" & Target.row).value = "0"
            End If
        End If
    End If
    
    'Credit GST/TPS
    If Not Intersect(Target, Range("L9:L23")) Is Nothing Then
        If Range("I" & Target.row).value <> 0 Then
            Select Case Range("H" & Target.row).value
                Case "FP"
                    Target.value = Range("J" & Target.row).value
                Case "REP"
                    Target.value = Round(Range("J" & Target.row).value * 0.5, 2)
                Case "F"
                    Target.value = Range("J" & Target.row).value
                Case "P"
                    Target.value = 0
                Case Else
                    Target.value = 0
            End Select
        Else
            Range("L" & Target.row).value = ""
        End If
    End If
    
    'Credit PST/TVQ
    If Not Intersect(Target, Range("M9:M23")) Is Nothing Then
        If Range("I" & Target.row).value <> 0 Then
            Select Case Range("H" & Target.row).value
                Case "FP"
                    Target.value = Range("K" & Target.row).value
                Case "REP"
                    Target.value = Round(Range("K" & Target.row).value * 0.5, 2)
                Case "F"
                    Target.value = 0
                Case "P"
                    Target.value = Range("K" & Target.row).value
                Case Else
                    Target.value = 0
            End Select
        Else
            Range("M" & Target.row).value = ""
        End If
    End If

    'Calculate the NET amount (Expense), but also accept user input
    If Not Intersect(Target, Range("N9:O23")) Is Nothing Then
        If Range("I" & Target.row).value <> 0 Then
            Range("N" & Target.row).formula = "=round(RC[-5]-rc[-2]-rc[-1],2)"
'        Else
'            Range("N" & Target.row).value = ""
        End If
    End If
    
    'Save the current cell Address
    previousCellAddress = Target.Address
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim fullDate As Variant
    
    'Si la cellule TYPE change, alors on vérifie si l'on essaie _
    '                          d'appeler une transaction récurrente
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        Application.EnableEvents = False
        If UCase(Trim(Range("F4").value)) = "AUTO" Then
            wshDEB_Saisie.Range("B3").value = -1
            ufListeDEBAuto.show
            If wshDEB_Saisie.Range("B3").value >= 0 Then
                wshDEB_Saisie.Range("O4").Activate
                wshDEB_Saisie.Range("O4").Select
            Else
                Call DEB_Saisie_Clear_All_Cells
            End If
        End If
        Application.EnableEvents = True
    End If
    
    'Date has changed
    If Not Application.Intersect(Target, Range("O4")) Is Nothing Then
        'Temporarily disable events to prevent infinite loop
        Application.EnableEvents = False
        
        'Try to convert the input to a complete valid date
        fullDate = CompleteDate(CStr(Target.text))
        
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            Target.value = fullDate
        Else
            Call MsgBoxInvalidDate
            Application.EnableEvents = False
            Target.Clearcontents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
        Application.EnableEvents = True
    End If

    'Modification de la description du compte et sauvegarde du no de compte
    If Not Intersect(Target, Range("E9:E23")) Is Nothing Then
        If wshDEB_Saisie.Range("E" & Target.row).value <> "" Then
            Application.EnableEvents = False
            wshDEB_Saisie.Range("Q" & Target.row).value = _
                Fn_Get_GL_Code_From_GL_Description(wshDEB_Saisie.Range("E" & Target.row).value)
            Application.EnableEvents = True
        End If
    End If
    
End Sub
