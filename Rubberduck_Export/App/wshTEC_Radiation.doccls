Option Explicit

Public clientCode As String

Private Sub Worksheet_Activate() '2024-09-29 @ 07:23

    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshTEC_Radiation:Worksheet_Activate", vbNullString, 0)
    
    If gFromMenu = False Then
        Exit Sub
    End If

    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    Dim ws As Worksheet: Set ws = wshTEC_Radiation
    
    'Import transactions from MASTER file
    Call modImport.ImporterClients
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100

    Me.Application.Calculation = xlCalculationAutomatic

    'Masquer (si nécessaire) les colonnes A et B
    ws.Unprotect
    Dim colsHidden As Boolean
    colsHidden = ws.Columns("A:B").Hidden
    If colsHidden = False Then
        ws.Range("A:B").EntireColumn.Hidden = True
    End If
    ws.Range("I3").Value = vbNullString
    
    'Certaines cellules perdent la couleur du background
    Dim cellsToColor As Range
    Application.EnableEvents = False
    Set cellsToColor = Union(ws.Range("E3"), ws.Range("F4"), ws.Range("I3"), ws.Range("J3"), ws.Range("L3"), ws.Range("F4"))
    Call modAppli_Utils.RemplirPlageAvecCouleur(cellsToColor, gCOULEUR_BASE_TEC)
    Application.EnableEvents = True
    
    Call modDev_Utils.DeterminerOrdreDeTabulation(ws)
        
    Call PreparerNouvelleRadiation
    
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With

    Application.EnableEvents = True
    
    ws.Visible = xlSheetVisible
    
    gFromMenu = True
    
    'Libérer la mémoire
    Set ws = Nothing
    
    Call modDev_Utils.EnregistrerLogApplication("wshTEC_Radiation:Worksheet_Activate", vbNullString, startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshTEC_Radiation:Worksheet_SelectionChange", vbNullString, 0)
    
    If gPreviousCellAddress <> vbNullString Then
        Range(gPreviousCellAddress).Interior.Color = vbWhite
    End If
    
    'Is client been selected ?
    If Not Intersect(Target, wshTEC_Radiation.Range("F3")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        If wshTEC_Radiation.Range("E6").Value <> vbNullString Then
            Call PreparerNouvelleRadiation
        End If
    End If
    
    'Is date been selected ?
    If Not Intersect(Target, wshTEC_Radiation.Range("K3")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
    End If
    
    gPreviousCellAddress = Target.Address
    
    Call modDev_Utils.EnregistrerLogApplication("wshTEC_Radiation:Worksheet_SelectionChange", vbNullString, startTime)
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshTEC_Radiation:Worksheet_Change", Target.Address, 0)
    
    Dim fullDate As Variant
    
    'Le client a changé
    If Not Intersect(Target, Range("F3")) Is Nothing Then
        'Aller chercher le vrai nom du client
        Dim allCols As Variant
        allCols = Fn_ObtenirLigneDeFeuille("BD_Clients", Target.Value, fClntFMNomClientPlusNomClientSystème)
        'Vérifier les résultats
        If IsArray(allCols) Then
            Application.EnableEvents = False
            Target.Value = allCols(1)
            Application.EnableEvents = True
        Else
            MsgBox "Valeur non trouvée !!!", vbCritical
            Exit Sub
        End If
        Application.EnableEvents = False
        Target.Interior.Color = gCOULEUR_BASE_TEC
        gPreviousCellAddress = Target.Address
        clientCode = allCols(fClntFMClientID)
        Range("K3").Value = ""
        Application.EnableEvents = True
        If Fn_Is_Client_Facturable(clientCode) = False Then
            MsgBox "Ce client n'est pas un client facturable", vbInformation
            Target.Activate
        End If
        'Déplacement explicite vers K3
        Me.Range("K3").Select

    End If
    
    'CutOffDate has changed
    If Not Intersect(Target, Range("K3")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_CompleteLaDate(Target.text, 99, 0)
        If fullDate <> "Invalid Date" Then
            wshTEC_Radiation.Range("I3").Value = vbNullString
            Target.Value = Format$(fullDate, wsdADMIN.Range("B1").Value)
            Target.Interior.Color = vbWhite
       Else
            Call modDataValidation.AfficherMessageDateInvalide("wshTEC_Radiation_75")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
       
        'Future date ?
        If CDate(Range("K3").Value) > Date Then
            If MsgBox("Il n'est pas permis d'utiliser une date dans le futur !", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Application.EnableEvents = False
                Target.ClearContents
                Application.EnableEvents = True
                Application.Goto Range(Target.Address)
            End If
        Else
            Call AfficherTECARadier(clientCode, Target.Value)
        End If
        
    End If

exitSub:

    Application.EnableEvents = True
    
    Call modDev_Utils.EnregistrerLogApplication("wshTEC_Radiation:Worksheet_Change", vbNullString, startTime)
  
End Sub
