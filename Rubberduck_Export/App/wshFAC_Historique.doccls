Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate()

    If fromMenu = False Then
        Debug.Print "#011 - wshFAC_Brouillon_Worksheet_Activate - Je ne viens pas du menu"
        Exit Sub
    End If
    
    Dim startTime As Double: startTime = Timer: Call Log_Record("wshFAC_Historique:Worksheet_Activate", 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call FAC_Comptes_Clients_Import_All
    Call FAC_Entête_Import_All
    Call FAC_Détails_Import_All
    
    Call FAC_Historique_Clear_All_Cells
    
    Dim shp As Shape: Set shp = wshFAC_Historique.Shapes("shpAfficheFactures")
    shp.Visible = False
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100

    Dim ws As Worksheet: Set ws = wshFAC_Historique
    
    ws.Application.Calculation = xlCalculationAutomatic
    
'    ws.Unprotect
    
    Call SetTabOrder(ws)
    
    With ws
        .Range("D4").Select
        previousCellAddress = .Range("D4").Address
    End With
    
    Application.EnableEvents = True
    
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    'Pas de date pour commencer
    Application.EnableEvents = False
    ws.Range("G6,I6").ClearContents
    Application.EnableEvents = True
    
    'Libérer la mémoire
    Set shp = Nothing
    Set ws = Nothing
    
    Call Log_Record("wshFAC_Historique:Worksheet_Activate", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.color = xlNone
    End If

    'Client has been selected
    If Not Intersect(Target, wshFAC_Historique.Range("D4")) Is Nothing Then
        Target.Interior.color = HIGHLIGHT_COLOR
    End If

    'Dates period has been selected
    If Not Intersect(Target, wshFAC_Historique.Range("D6")) Is Nothing Then
        Target.Interior.color = HIGHLIGHT_COLOR
    End If

    'Dates from has been selected
    If Not Intersect(Target, wshFAC_Historique.Range("G6")) Is Nothing Then
        Target.Interior.color = HIGHLIGHT_COLOR
    End If

    'Dates to has been selected
    If Not Intersect(Target, wshFAC_Historique.Range("I6")) Is Nothing Then
        Target.Interior.color = HIGHLIGHT_COLOR
    End If

    'Save the current cell Address
    previousCellAddress = Target.Address

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim fullDate As Variant
    
    'Client name change
     If Not Intersect(Target, Range("D4")) Is Nothing Then
        Call FAC_Historique_Montrer_Bouton
        Range(Target.Address).Interior.color = xlNone
        Application.Goto Range("D6")
     End If
   
    'Dates Period change
    If Not Intersect(Target, Range("D6")) Is Nothing Then
        Application.EnableEvents = False
        Select Case Range("D6").Value
            Case "Aujourd'hui"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("Aujourdhui")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("Aujourdhui")
            Case "Mois Courant"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("MoisDe")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("MoisA")
            Case "Mois Dernier"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("MoisPrecDe")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("MoisPrecA")
            Case "Trimestre courant"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("TrimDe")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("TrimA")
            Case "Trimestre précédent"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("TrimPrecDe")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("TrimPrecA")
            Case "Année courante"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("AnneeDe")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("AnneeA")
            Case "Année précédente"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("AnneePrecDe")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("AnneePrecA")
            Case "7 derniers jours"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("SeptJoursDe")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("SeptJoursA")
            Case "15 derniers jours"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("QuinzeJoursDe")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("QuinzeJoursA")
            Case "Semaine"
                wshFAC_Historique.Range("G6").Value = wshAdmin.Range("DateDebutSemaine")
                wshFAC_Historique.Range("I6").Value = wshAdmin.Range("DateFinSemaine")
            Case Else
                wshFAC_Historique.Range("G6").Value = ""
                wshFAC_Historique.Range("I6").Value = ""
        End Select
        Call FAC_Historique_Montrer_Bouton
        Application.EnableEvents = True
    End If
    
    'From date has changed
    If Not Intersect(Target, Range("G6")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(Target.Text, 999, 0)
        
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            Target.Value = Format$(fullDate, wshAdmin.Range("B1").Value)
            Call FAC_Historique_Montrer_Bouton
'            wshFAC_Historique.Range("C9:P33").ClearContents
'            Call Remove_All_PDF_Icons
            Application.EnableEvents = True
        Else
            Call modTEC_Saisie.AfficherMessageDateInvalide("wshFAC_Historique_179")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
    End If
    
    'To date has changed
    If Not Intersect(Target, Range("I6")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(Target.Text, 999, 366)
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            Target.Value = Format$(fullDate, wshAdmin.Range("B1").Value)
            Call FAC_Historique_Montrer_Bouton
'            Call Remove_All_PDF_Icons
            Application.EnableEvents = True
        Else
            Call modTEC_Saisie.AfficherMessageDateInvalide("wshFAC_Historique_199")
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
    End If

End Sub
