Option Explicit

Public clientCode As String

Private Sub Worksheet_Activate() '2024-09-29 @ 07:23

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshTEC_Radiation:Worksheet_Activate", "", 0)
    
    Call RafraichirActivite("Radiation des TEC")
    
    If gFromMenu = False Then
        Exit Sub
    End If

    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    Dim ws As Worksheet: Set ws = wshTEC_Radiation
    
    'Import transactions from MASTER file
    Call modImport.ImporterClients
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100

    Me.Application.Calculation = xlCalculationAutomatic

    'Masquer (si nécessaire) les colonnes A et B
    ws.Unprotect
    Dim colsHidden As Boolean
    colsHidden = ws.Columns("A:B").Hidden
    If colsHidden = False Then
        ws.Range("A:B").EntireColumn.Hidden = True
    End If
    ws.Range("I3").Value = ""
    
    'Certaines cellules perdent la couleur du background
    Dim cellsToColor As Range
    Application.EnableEvents = False
    Set cellsToColor = Union(ws.Range("E3"), ws.Range("F4"), ws.Range("I3"), ws.Range("J3"), ws.Range("L3"), ws.Range("F4"))
    Call Remplir_Plage_Avec_Couleur(cellsToColor, gCOULEUR_BASE_TEC)
    Application.EnableEvents = True
    
    Call SetTabOrder(ws)
        
    Call Prepare_Pour_Nouvelle_Radiation
    
    With ws
        .Protect userInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With

    Application.EnableEvents = True
    
    ws.Visible = xlSheetVisible
    
    gFromMenu = True
    
    'Libérer la mémoire
    Set ws = Nothing
    
    Call Log_Record("wshTEC_Radiation:Worksheet_Activate", "", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshTEC_Radiation:Worksheet_SelectionChange", "", 0)
    
    Call RafraichirActivite("Radiation des TEC")
    
    If gPreviousCellAddress <> "" Then
        Range(gPreviousCellAddress).Interior.Color = vbWhite
    End If
    
    'Is client been selected ?
    If Not Intersect(Target, wshTEC_Radiation.Range("F3")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
    End If
    
    'Is date been selected ?
    If Not Intersect(Target, wshTEC_Radiation.Range("K3")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
    End If
    
    gPreviousCellAddress = Target.Address
    
    Call Log_Record("wshTEC_Radiation:Worksheet_SelectionChange", "", startTime)
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshTEC_Radiation:Worksheet_Change", Target.Address, 0)
    
    Call RafraichirActivite("Radiation des TEC")
    
    Dim fullDate As Variant
    
    'Le client a changé
    If Not Intersect(Target, Range("F3")) Is Nothing Then
        'Aller chercher le vrai nom du client
        Dim allCols As Variant
        allCols = Fn_Get_A_Row_From_A_Worksheet("BD_Clients", Target.Value, fClntFMNomClientPlusNomClientSystème)
        'Vérifier les résultats
        If IsArray(allCols) Then
            Application.EnableEvents = False
            Target.Value = allCols(1)
            Application.EnableEvents = True
        Else
            MsgBox "Valeur non trouvée !!!", vbCritical
        End If
        Application.EnableEvents = False
        Target.Interior.Color = gCOULEUR_BASE_TEC
        gPreviousCellAddress = Target.Address
        clientCode = allCols(fClntFMClientID)
        Application.EnableEvents = True
        If Fn_Is_Client_Facturable(clientCode) = False Then
            MsgBox "Ce client n'est pas un client facturable", vbInformation
            Target.Activate
        End If
        'Déplacement explicite vers K3
        Me.Range("K3").Select

    End If
    
    'CutOffDate has changed
    If Not Intersect(Target, Range("K3")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(Target.Text, 99, 0)
        If fullDate <> "Invalid Date" Then
            wshTEC_Radiation.Range("I3").Value = ""
            Target.Value = Format$(fullDate, wsdADMIN.Range("B1").Value)
            Target.Interior.Color = vbWhite
       Else
            Call modTEC_Saisie.AfficherMessageDateInvalide("wshTEC_Radiation_75")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
       
        'Future date ?
        If CDate(Range("L3").Value) > Date Then
            If MsgBox("Il n'est pas permis d'utiliser une date dans le futur !", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Application.EnableEvents = False
                Target.ClearContents
                Application.EnableEvents = True
                Application.Goto Range(Target.Address)
            End If
        Else
            Call TEC_Radiation_Procedure(clientCode, Target.Value)
        End If
        
    End If

ExitSub:

    Application.EnableEvents = True
    
    Call Log_Record("wshTEC_Radiation:Worksheet_Change", "", startTime)
  
End Sub

Sub SupprimerFormesFantômesImprimables()
    Dim shp As Shape
    Dim nomsProtegés As Variant
    Dim n As Variant
    Dim nomForme As String
    Dim estProtégée As Boolean  ' ? Déclaration manquante ajoutée ici

    nomsProtegés = Array("Impression", "Radiation", "Retour au menu")

    For Each shp In ActiveSheet.Shapes
        estProtégée = False

        ' Tente d'accéder au nom de la forme
        On Error Resume Next
        nomForme = shp.Name
        Debug.Print nomForme
        On Error GoTo 0

        For Each n In nomsProtegés
            If nomForme = n Then
                estProtégée = True
                Exit For
            End If
        Next n

        If Not estProtégée Then
            On Error Resume Next
            
            If shp.Type = msoAutoShape Or shp.Type = msoTextBox Or shp.Type = msoPicture Then
                If shp.Visible = msoFalse _
                Or (Not shp.Fill Is Nothing And shp.Fill.Transparency = 1) _
                Or (shp.PrintObject = True And shp.Type <> msoPicture And shp.Type <> msoTextBox) Then
                    shp.Delete
                End If
            End If
            
            On Error GoTo 0
        End If
    Next shp

    MsgBox "Nettoyage terminé. Les 3 formes officielles ont été préservées ??", vbInformation
End Sub











