'@Folder("Rapport_ÉtatsFinanciers")

Option Explicit

Private Sub Worksheet_Activate()

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshGL_PrepEF:Worksheet_Activate", "", 0)

    Call RafraichirActivite("États financiers")
    
    Dim ws As Worksheet: Set ws = wshGL_PrepEF
    
    ws.Unprotect
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Me.Application.Calculation = xlCalculationAutomatic
    
    'Import transactions from MASTER file
    Call modImport.ImporterGLTransactions

    'Set the zoom factor to 95% when this worksheet is activated
    ActiveWindow.Zoom = 100

    'Cache le bouton 'Actualiser'
    Dim shp As Shape
    Set shp = ws.Shapes("Actualiser")
    shp.Visible = False
    
    'Clear the display area & display the account number & description
    Dim lastUsedRow As Long
    lastUsedRow = ws.Cells(ws.Rows.count, "F").End(xlUp).Row
    If lastUsedRow > 5 Then
        ws.Range("C6:S" & lastUsedRow).ClearContents
    End If
    
    'Protection de la feuille
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    'Date du jour par défaut
    gPreviousCellAddress = ws.Range("F3").Address
    ws.Range("F3").Value = ""
'    ws.Range("F3").Value = Format$(Date, wsdADMIN.Range("B1").Value)
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    ws.Range("F3").Select
    
    'Libérer la mémoire
    Set ws = Nothing
    
    Call Log_Record("wshGL_PrepEF:Worksheet_Activate", "", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    Call RafraichirActivite("États financiers")
    
    If gPreviousCellAddress <> "" Then
        Range(gPreviousCellAddress).Interior.Color = xlNone
    End If

    'Date limite est sélectionnée
    If Not Intersect(Target, Me.Range("F3")) Is Nothing Then
        Target.Interior.Color = COULEUR_SAISIE
    End If
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim fullDate As Variant
    
    'Date limite a été changée
    If Not Intersect(Target, Range("F3")) Is Nothing Then
        
        'Pour éviter des boucles infinies
        Application.EnableEvents = False
        
        'Validation de la date
        fullDate = Fn_Complete_Date(Target.Value, 999, 15)
        'Date est-elle valide ?
        If fullDate <> "Invalid Date" Then
            Target.Value = Format$(fullDate, wsdADMIN.Range("B1").Value)
        Else
            Call modTEC_Saisie.AfficherMessageDateInvalide("wshGL_PrepEF_139")
            Target.ClearContents
            Application.Goto Range(Target.Address)
        End If
        
        Application.EnableEvents = True
        
        Dim ws As Worksheet
        Set ws = wshGL_PrepEF
        
        Call Calculer_Soldes_Pour_EF(ws, Range("F3").Value)
        
        'Afficher le bouton 'Actualiser'
        Dim shp As Shape
        Set shp = ws.Shapes("Actualiser")
        shp.Visible = True
        
    End If

End Sub

