'@Folder("Rapport_ÉtatsFinanciers")

Option Explicit

Private Sub Worksheet_Activate()

    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshGL_PrepEF:Worksheet_Activate", vbNullString, 0)

    Dim ws As Worksheet: Set ws = wshGL_PrepEF
    
    ws.Unprotect
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Me.Application.Calculation = xlCalculationAutomatic
    
    'Import transactions from MASTER file
    Call modImport.ImporterGLTransactions

    'Set the zoom factor to 95% when this worksheet is activated
    ActiveWindow.Zoom = 100

    'Cache le bouton 'Actualiser'
    Dim shp As Shape
    Set shp = ws.Shapes("shpPreparationEtatsFinanciers")
    shp.Visible = False
    
    'Clear the display area & display the account number & description
    Dim lastUsedRow As Long
    lastUsedRow = ws.Cells(ws.Rows.count, "F").End(xlUp).Row
    If lastUsedRow > 5 Then
        ws.Range("C6:S" & lastUsedRow).ClearContents
    End If
    
    ws.Range("F5, H5").Value = ""
    
    'Protection de la feuille
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    'Date du jour par défaut
    gPreviousCellAddress = ws.Range("J3").Address
    ws.Range("J3").Value = vbNullString
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    ws.Range("J3").Select
    
    'Libérer la mémoire
    Set ws = Nothing
    
    Call modDev_Utils.EnregistrerLogApplication("wshGL_PrepEF:Worksheet_Activate", vbNullString, startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If gPreviousCellAddress <> vbNullString Then
        Range(gPreviousCellAddress).Interior.Color = xlNone
    End If

    'Date limite est sélectionnée
    If Not Intersect(Target, Me.Range("J3")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
    End If
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim fullDate As Variant
    
    'Date limite a été changée
    If Not Intersect(Target, Range("J3")) Is Nothing Then
        
        'Pour éviter des boucles infinies
        Application.EnableEvents = False
        
        'Validation de la date
        fullDate = Fn_CompleteLaDate(Target.Value, 999, 15)
        'Date est-elle valide ?
        If fullDate <> "Invalid Date" Then
            Target.Value = Format$(fullDate, wsdADMIN.Range("B1").Value)
        Else
            Call modDataValidation.AfficherMessageDateInvalide("wshGL_PrepEF_139")
            Target.ClearContents
            Application.Goto Range(Target.Address)
        End If
        
        Application.EnableEvents = True
        
        Dim ws As Worksheet
        Set ws = wshGL_PrepEF
        
        Call CalculerSoldesPourEF(ws, Range("J3").Value)
        
        'Afficher le bouton 'Actualiser'
        Dim shp As Shape
        Set shp = ws.Shapes("shpPreparationEtatsFinanciers")
        shp.Visible = True
        
    End If

    Application.EnableEvents = True

End Sub
