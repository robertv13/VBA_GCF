Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate()

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshFAC_Confirmation:Worksheet_Activate", 0)
    
    Application.EnableEvents = False
    
    Call TEC_Import_All
    Call FAC_Entête_Import_All
    Call FAC_Détails_Import_All
    Call FAC_Sommaire_Taux_Import_All
    Call GL_Trans_Import_All
    
    Dim ws As Worksheet: Set ws = wshFAC_Confirmation
    
    'Si la feuille est masquée, la rendre visible
    If ws.Visible = xlSheetHidden Or ws.Visible = xlSheetVeryHidden Then
        ws.Visible = xlSheetVisible
    End If
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100
    
    Me.Application.Calculation = xlCalculationAutomatic

    On Error Resume Next
    ws.Range("A:B").EntireColumn.Hidden = True
    On Error GoTo 0
    
    Dim lastRow As Long
    lastRow = ws.Cells(ws.Rows.count, "P").End(xlUp).row
    Dim r As Range
    Set r = ws.Range("P4:P" & lastRow)
    On Error Resume Next
    r.Locked = True
    On Error GoTo 0
    
    'Protéger la feuille sans mot de passe et permettre la sélection des cellules verrouillées et non verrouillées
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlNoRestrictions
    End With
    
    Call SetTabOrder(ws)
    
    'Hide the CONFIRM and OK buttons
    ws.Shapes("shpConfirmerFacture").Visible = False
    ws.Shapes("shpOK").Visible = False
    
    ws.Activate
    
    previousCellAddress = ws.Range("F5").Address
    Call NettoyerCellulesEtIconesPDF
    
    ws.Range("F5").value = ""
    ws.Range("F5").Select
    
    Application.EnableEvents = True

    'Libérer la mémoire
    Set r = Nothing
    Set ws = Nothing
    
    Call Log_Record("wshFAC_Confirmation:Worksheet_Activate", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.color = xlNone
    End If
    
    'Invoice Number is selected
    If Not Intersect(Target, Range("F5")) Is Nothing And Target.Cells.count = 1 Then
        Target.Interior.color = HIGHLIGHT_COLOR
    End If
    
    'Invoice Number is selected from Invoices to be confirmed
    If Not Intersect(Target, Me.Columns(16)) Is Nothing And Target.Cells.count = 1 Then
        Call NettoyerCellulesEtIconesPDF
        Call TraiterToutesLesFacturesAC(Target.value)
        wshFAC_Confirmation.Range("F5").value = Target.value
        Target.Interior.color = HIGHLIGHT_COLOR
    End If
    
    'Save the current cell Address
    previousCellAddress = Target.Address
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    
    Debug.Print "#010 - Worksheet_Change Event - " & Target.Address & " = " & Target.value
    
    'Saisie du numéro de facture
    If Not Intersect(Target, Range("F5")) Is Nothing Then
        invNo = Trim(Target.value)
        If invNo <> "" Then
            Call ObtenirFactureInfos(invNo)
        Else
            On Error Resume Next
            Target.Select
            On Error GoTo 0
        End If
    End If

    'Sélection d'une facture à confirmer
    If Not Intersect(Target, Range("P4:P999")) Is Nothing Then
        invNo = Trim(Target.value)
        If invNo <> "" Then
            Call ObtenirFactureInfos(invNo)
        Else
            Exit Sub
        End If
    End If

End Sub
