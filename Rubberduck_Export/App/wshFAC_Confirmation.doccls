Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate()

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshFAC_Confirmation:Worksheet_Activate", 0)
    
    Application.EnableEvents = False
    
    Call TEC_Import_All
    Call FAC_Entête_Import_All
    Call FAC_Détails_Import_All
    Call FAC_Sommaire_Taux_Import_All
    
    Dim ws As Worksheet: Set ws = wshFAC_Confirmation
    
    'Si la feuille est masquée, la rendre visible
    If ws.Visible = xlSheetHidden Or ws.Visible = xlSheetVeryHidden Then
        ws.Visible = xlSheetVisible
    End If
    
    'Set the zoom factor to 95% when this worksheet is activated
    ActiveWindow.Zoom = 95
    
    Me.Application.Calculation = xlCalculationAutomatic

    On Error Resume Next
    ws.Range("A:B").EntireColumn.Hidden = True
    On Error GoTo 0
    
    Dim lastRow As Long
    lastRow = ws.Cells(ws.rows.count, "P").End(xlUp).Row
    Dim r As Range
    Set r = ws.Range("P4:P" & lastRow)
    On Error Resume Next
    r.Locked = True
    On Error GoTo 0
    
    'Protéger la feuille sans mot de passe et permettre la sélection des cellules verrouillées et non verrouillées
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlNoRestrictions
    End With
    
    Call SetTabOrder(ws)
    
'    Application.EnableEvents = False
    
    'Hide the OK and CANCEL buttons
    ws.Shapes("btnFAC_Confirmation_OK").Visible = False
    
    ws.Activate
    
    Call FAC_Confirmation_Clear_Cells_And_PDF_Icon
'    Call Show_Unconfirmed_Invoice
    
    previousCellAddress = ws.Range("F5").Address
    Call FAC_Confirmation_Clear_Cells_And_PDF_Icon
    ws.Range("F5").value = ""
    
    ws.Range("F5").Select
    
    Application.EnableEvents = True

    'Libérer la mémoire
    Set r = Nothing
    Set ws = Nothing
    
    Call Log_Record("wshFAC_Confirmation:Worksheet_Activate", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = xlNone
    End If
    
    'Invoice Number is selected
    If Not Intersect(Target, Range("F5")) Is Nothing And Target.Cells.count = 1 Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Invoice Number is selected from Invoices to be confirmed
    If Not Intersect(Target, Me.columns(16)) Is Nothing And Target.Cells.count = 1 Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Save the current cell Address
    previousCellAddress = Target.Address
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    
    Debug.Print "Worksheet_Change Event - " & Target.Address & " = " & Target.value
    
    'Saisie du numéro de facture
    If Not Intersect(Target, Range("F5")) Is Nothing Then
        invNo = Trim(Target.value)
        If invNo <> "" Then
            Call Get_Invoice_Data(invNo)
        Else
            On Error Resume Next
            Target.Select
            On Error GoTo 0
        End If
    End If

    'Sélection d'une facture à confirmer
    If Not Intersect(Target, Range("P4:P999")) Is Nothing Then
        invNo = Trim(Target.value)
        If invNo <> "" Then
            Call Get_Invoice_Data(invNo)
        Else
            Exit Sub
        End If
    End If

End Sub

Sub FAC_Confirmation_Back_To_FAC_Menu()
    
    Dim startTime As Double: startTime = Timer: Call Log_Record("modFAC_Confirmation:Back_To_FAC_Menu", 0)
   
    wshFAC_Confirmation.Unprotect '2024-08-21 @ 05:06
    
    Application.EnableEvents = False
    wshFAC_Confirmation.Range("F5").ClearContents
    Application.EnableEvents = True
    
    wshFAC_Confirmation.Visible = xlSheetHidden

    wshMenuFAC.Activate
    wshMenuFAC.Range("A1").Select
    
    Call Log_Record("modFAC_Confirmation:Back_To_FAC_Menu", startTime)
    
End Sub
