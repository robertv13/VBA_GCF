Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate()
    
    Dim timerStart As Double: timerStart = Timer: Call Start_Routine("wshGL_Rapport:Worksheet_Activate()")
    
    Application.ScreenUpdating = False
    
    Dim ws As Worksheet
    Set ws = ThisWorkbook.Sheets("GL_Rapport")
    ws.Unprotect
    
    'Setup a listBox
    Dim lb As OLEObject
    Set lb = Me.OLEObjects("ListBox1")
    
    'Ensure it is a ListBox
    If TypeName(lb.Object) = "ListBox" Then
        With lb.Object
            'Clear existing items
            .clear

            Dim arr As Variant
            arr = Fn_Get_Chart_Of_Accounts(2) 'Returns 2D array with 2 columns
            
            'Add items to the ListBox
            Dim i As Integer
            For i = LBound(arr, 1) To UBound(arr, 1)
                .AddItem arr(i, 1) & " " & arr(i, 2)
            Next i
        End With
    End If

    'Set the zoom factor to 95% when this worksheet is activated
    ActiveWindow.Zoom = 95

    Application.EnableEvents = False
    
    'Hide column A & B
    With ws
'        .Range("A:B").EntireColumn.Hidden = True
        .Protect UserInterfaceOnly:=True
        .EnableCalculation = True
    End With
    
    Call GL_Rapport_Clear_All_Cells(ws)
    
    Application.EnableEvents = True
    
    Call Output_Timer_Results("wshGL_Rapport:Worksheet_Activate()", timerStart)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = xlNone
    End If
 
    'Dates period has ben selected
    If Not Intersect(Target, wshGL_Rapport.Range("F4")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'From Date has ben selected
    If Not Intersect(Target, wshGL_Rapport.Range("F6")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'To Date has ben selected
    If Not Intersect(Target, wshGL_Rapport.Range("H6")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Save the current cell Address
    previousCellAddress = Target.Address

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim fullDate As Variant
    
    'Period change
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        Application.EnableEvents = False
        Select Case Range("F4").value
            Case "Aujourd'hui"
                wshGL_Rapport.Range("F6").value = Format(Now(), "dd-mm-yyyy")
                wshGL_Rapport.Range("H6").value = Format(Now(), "dd-mm-yyyy")
            Case "Mois Courant"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("MoisDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("MoisA")
            Case "Mois Dernier"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("MoisPrecDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("MoisPrecA")
            Case "Trimestre courant"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("TrimDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("TrimA")
            Case "Trimestre précédent"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("TrimPrecDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("TrimPrecA")
            Case "Année courante"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("AnneeDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("AnneeA")
            Case "Année précédente"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("AnneePrecDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("AnneePrecA")
            Case "7 derniers jours"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("SeptJoursDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("SeptJoursA")
            Case "15 derniers jours"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("QuinzeJoursDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("QuinzeJoursA")
            Case "Semaine"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("DateDebutSemaine")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("DateFinSemaine")
            Case Else
                wshGL_Rapport.Range("F6").value = ""
                wshGL_Rapport.Range("H6").value = ""
        End Select
        Application.EnableEvents = True
    End If
    
    'From date has changed
    If Not Intersect(Target, Range("F6")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = CompleteDate(CStr(Target.text))
        
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            Target.value = fullDate
        Else
            Call MsgBoxInvalidDate
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
    End If
    
    'To date has changed
    If Not Intersect(Target, Range("H6")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = CompleteDate(CStr(Target.text))
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            Target.value = fullDate
        Else
            Call MsgBoxInvalidDate
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
    End If

Clean_Exit:

        Application.EnableEvents = True

End Sub

Private Sub cbTous_Click()

    Dim lb As OLEObject
    Set lb = Me.OLEObjects("ListBox1")
    
    Dim i As Long
    With lb.Object
        For i = 0 To .ListCount - 1
            .Selected(i) = Not (.Selected(i))
        Next i
    End With

End Sub
