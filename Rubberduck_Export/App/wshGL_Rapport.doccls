Option Explicit

Private Sub Worksheet_Activate()
    
    Dim timerStart As Double: timerStart = Timer: Call Start_Routine("wshGL_Rapport:Worksheet_Activate()")
    
    Dim ws As Worksheet
    Set ws = ThisWorkbook.Sheets("GL_Rapport")
    
    Dim lb As OLEObject
    Set lb = Me.OLEObjects("ListBox1")
    
    'Ensure it is a ListBox
    If TypeName(lb.Object) = "ListBox" Then
        With lb.Object
            'Clear existing items
            .clear

            Dim arr As Variant
            arr = Fn_Get_Chart_Of_Accounts(2) 'Returns 2D array with 2 columns
            
            'Add items to the ListBox
            Dim i As Integer
            For i = LBound(arr, 1) To UBound(arr, 1)
                .AddItem arr(i, 1) & " " & arr(i, 2)
            Next i
        End With
    End If

    Application.EnableEvents = False
    
    With ws
        .Range("B3").value = True 'Sort by Date
        .Range("F4").value = "Dates manuelles"
        .Range("F6").value = ""
        .Range("H6").value = ""
        .Range("F4").Activate
        .Range("F4").Select
    End With
    
    Application.EnableEvents = True
    
    Call Output_Timer_Results("wshGL_Rapport:Worksheet_Activate()", timerStart)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    'From Date has ben selected
    If Not Intersect(Target, wshGL_Rapport.Range("F6")) Is Nothing And _
                             wshGL_Rapport.Range("F6").value = "" Then
        wshGL_Rapport.Range("F6").value = Format(Now(), "dd-mm-yyyy")
    End If
    
    'To Date has ben selected
    If Not Intersect(Target, wshGL_Rapport.Range("H6")) Is Nothing And _
                             wshGL_Rapport.Range("H6").value = "" Then
        wshGL_Rapport.Range("H6").value = Format(Now(), "dd-mm-yyyy")
    End If

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    'Period change
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        Application.EnableEvents = False
        Select Case Range("F4").value
            Case "Aujourd'hui"
                wshGL_Rapport.Range("F6").value = Format(Now(), "dd-mm-yyyy")
                wshGL_Rapport.Range("H6").value = Format(Now(), "dd-mm-yyyy")
            Case "Mois Courant"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("MoisDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("MoisA")
            Case "Mois Dernier"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("MoisPrecDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("MoisPrecA")
            Case "Trimestre courant"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("TrimDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("TrimA")
            Case "Trimestre précédent"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("TrimPrecDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("TrimPrecA")
            Case "Année courante"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("AnneeDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("AnneeA")
            Case "Année précédente"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("AnneePrecDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("AnneePrecA")
            Case "7 derniers jours"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("SeptJoursDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("SeptJoursA")
            Case "15 derniers jours"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("QuinzeJoursDe")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("QuinzeJoursA")
            Case "Semaine"
                wshGL_Rapport.Range("F6").value = wshAdmin.Range("DateDebutSemaine")
                wshGL_Rapport.Range("H6").value = wshAdmin.Range("DateFinSemaine")
            Case Else
                wshGL_Rapport.Range("F6").value = ""
                wshGL_Rapport.Range("H6").value = ""
        End Select
        Application.EnableEvents = True
    End If
    
    'From date has changed
    If Not Intersect(Target, Range("F6")) Is Nothing Then
        Application.EnableEvents = False
        Dim strFromDate As String
        strFromDate = Fn_Build_A_Date(Range("F6").value)
        If strFromDate = "" Then
            Call Invalid_Date_Message
            Range("F6").Activate
            Range("F6").Select
            GoTo Clean_Exit
        Else
            Range("F6").value = CDate(strFromDate)
        End If
    End If
    
    'To date has changed
    If Not Intersect(Target, Range("H6")) Is Nothing Then
        Application.EnableEvents = False
        Dim strToDate As String
        strToDate = Fn_Build_A_Date(Range("H6").value)
        If strToDate = "" Then
            Call Invalid_Date_Message
            Range("H6").Activate
            Range("H6").Select
            GoTo Clean_Exit
        Else
            Range("H6").value = CDate(strToDate)
        End If
    End If

Clean_Exit:
        Application.EnableEvents = True

End Sub

Private Sub cbTous_Click()

    Dim lb As OLEObject
    Set lb = Me.OLEObjects("ListBox1")
    
    Dim i As Long
    With lb.Object
        For i = 0 To .ListCount - 1
            .Selected(i) = Not (.Selected(i))
        Next i
    End With

End Sub
