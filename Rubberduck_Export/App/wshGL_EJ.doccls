Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate() '2024-06-13 @ 18:07

    Dim timerStart As Double: timerStart = Timer: Call Start_Routine("wshGL_EJ:Worksheet_Activate()")
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call GL_Trans_Import_All
    Call GL_EJ_Auto_Import_All
    Call wshGL_EJ_Clear_All_Cells
    
    'Set the zoom factor to 95% when this worksheet is activated
    ActiveWindow.Zoom = 95

    Me.Application.Calculation = xlCalculationAutomatic

    Dim ws As Worksheet
    Set ws = ThisWorkbook.Sheets("EJ")
    
   'Hide column A & B
    With ws
        .Unprotect
        .Range("A:B").EntireColumn.Hidden = True
    End With
    
    Call SetTabOrder(ws)
    
    ws.Range("F4").Activate
    ws.Range("F4").Select
    previousCellAddress = ws.Range("F4").Address
    
    Application.EnableEvents = True
    
    Call Output_Timer_Results("wshGL_EJ:Worksheet_Activate()", timerStart)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = xlNone
    End If

    'Source has been selected
    If Not Intersect(Target, wshGL_EJ.Range("F4")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'Date has been selected
    If Not Intersect(Target, wshGL_EJ.Range("K4")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'Description has been selected
    If Not Intersect(Target, wshGL_EJ.Range("F6")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'Compte has been selected
    If Not Intersect(Target, wshGL_EJ.Range("E9:E23")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Debit amount has been selected
    If Not Intersect(Target, wshGL_EJ.Range("H9:H23")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Credit amount has been selected
    If Not Intersect(Target, wshGL_EJ.Range("I9:I23")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Extra description has been selected
    If Not Intersect(Target, wshGL_EJ.Range("J9:J23")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Save the current cell Address
    previousCellAddress = Target.Address

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim fullDate As Variant
    
    'Si la cellule de la SOURCE change, alors on vérifie si l'on essaie d'appeler des E/J récurrentes
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        Application.EnableEvents = False
        If UCase(Trim(Range("F4").value)) = "AUTO" Then
            wshGL_EJ.Range("B2").value = -1
            ufListeEJAuto.show
            If wshGL_EJ.Range("B2").value >= 0 Then
                wshGL_EJ.Range("K4").Activate
                wshGL_EJ.Range("K4").Select
            Else
                Call wshGL_EJ_Clear_All_Cells
            End If
        End If
        Application.EnableEvents = True
    End If
    
    'Si la cellule de la DATE change, alors on essaie de construire une date
    If Not Intersect(Target, Range("K4")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = CompleteDate(CStr(Target.text))
        
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            Target.value = fullDate
        Else
            Call MsgBoxInvalidDate
            Application.EnableEvents = False
            Target.Clearcontents
            Application.EnableEvents = True
            Application.GoTo Range(Target.Address)
        End If
        
        If CDate(Me.Range("K4").value) > Format(Now(), "dd-mm-yyyy") Then
            If MsgBox("En êtes-vous CERTAIN ?", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Target.value = ""
                Target.Activate
                Target.Select
            End If
        End If
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, sauvegarde du no de compte & suggestion du montant résiduel
    If Not Intersect(Target, Range("E9:G23")) Is Nothing Then
        Application.EnableEvents = False
        With wshGL_EJ
            If .Range("E" & Target.row).value <> "" Then
                .Range("L" & Target.row).value = Fn_Get_GL_Code_From_GL_Description(.Range("E" & Target.row).value)
            End If
            
            If Target.row > 9 And _
                (.Range("H26").value <> 0 Or .Range("I26").value <> 0) And _
                .Range("H26").value <> .Range("I26").value Then
                    If .Range("H26").value > .Range("I26").value Then
                        .Range("I" & Target.row).value = .Range("H26").value - .Range("I26").value
                        Application.EnableEvents = True
                        .Range("I" & Target.row).Select
                        Application.EnableEvents = False
                    Else
                        .Range("H" & Target.row).value = .Range("I26").value - .Range("H26").value
                        Application.EnableEvents = True
                        .Range("H" & Target.row).Select
                        Application.EnableEvents = False
                    End If
            End If
        End With
        Application.EnableEvents = True
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(Target, Range("H9:I23")) Is Nothing Then
        If (wshGL_EJ.Range("H" & Target.row).value < 0 Or wshGL_EJ.Range("I" & Target.row).value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H26").value <> Range("I26").value Then
            With Range("H26:I26").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("H26:I26").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
    End If
End Sub

Private Sub ckbRecurrente_Click()

    If ckbRecurrente.value = True Then
        ckbRecurrente.BackColor = HIGHLIGHT_COLOR
    Else
        ckbRecurrente.BackColor = RGB(217, 217, 217)
    End If

End Sub
