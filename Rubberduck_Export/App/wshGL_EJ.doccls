'@Folder("Saisie_Entrée_Journal")

Option Explicit

Private Sub Worksheet_Activate() '2024-06-13 @ 18:08

    If gFromMenu = False Then
        Exit Sub
    End If
    
    Dim startTime As Double: startTime = Timer: Call modDev_Utils.EnregistrerLogApplication("wshGL_EJ:Worksheet_Activate", vbNullString, 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call modImport.ImporterGLTransactions
    Call modImport.ImporterEJRecurrente
    Call modImport.ImporterClients
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100

    Me.Application.Calculation = xlCalculationAutomatic

    Dim ws As Worksheet: Set ws = wshGL_EJ
    
    'S'assurer que les colonnes A & B sont masquées
    With ws
        .Unprotect
        .Range("A:B").EntireColumn.Hidden = True
        .Range("N:Y").EntireColumn.Hidden = True
    End With
    
    'Cette cellule peut aussi contenir "Client:"
    ws.Range("E6").Value = "Description:"
    
    'Certaines cellules perdent la couleur du background
    Dim cellsToColor As Range
    Set cellsToColor = Me.Range("G4:H4")
    Call modAppli_Utils.RemplirPlageAvecCouleur(cellsToColor, gCOULEUR_BASE_COMPTABILITE)
    
    Call modDev_Utils.DeterminerOrdreDeTabulation(ws)
    
    Call modGL_EJ.EffacerCellulesEJ
    
    'Rétablir le bouton "Mettre à jour / Renversement"
    Dim shp As Shape
    Set shp = ws.Shapes("shpMettreAJour")
    
    With shp
        .Left = 530
        .Width = 115
        .Height = 30
        .Fill.ForeColor.RGB = 12566463
        .TextFrame2.TextRange.text = "Mettre à jour"
        .TextFrame2.TextRange.Font.Fill.ForeColor.RGB = RGB(0, 0, 0) 'Noir
    End With
    
    Call modGL_EJ.SauvegarderFormeEJ(shp)
    
    With ws
        .Range("B2").Value = -1
        .Range("B3").Value = -1
        .Range("K4").Value = Format$(Date, wsdADMIN.Range("USER_DATE_FORMAT").Value)
        .Range("F4").Select
    End With
    
    'Protection de la feuille
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With

    Me.Range("$E$9:$K$23").Locked = True

    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    'Libérer la mémoire
    Set shp = Nothing
    Set ws = Nothing
    
    Call modDev_Utils.EnregistrerLogApplication("wshGL_EJ:Worksheet_Activate", vbNullString, startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    ActiveSheet.Unprotect
    
    If gPreviousCellAddress <> vbNullString Then
        If Not Range(gPreviousCellAddress) Is Nothing Then
            Range(gPreviousCellAddress).Interior.Color = xlNone
        End If
    End If
    
    'Ne pas permettre aucun changement à l'écriture originale, lors d'un renversement
    If Me.Range("F4").Value = "Renversement" Then
        MsgBox "Il n'est pas permis de modifier l'écriture à renverser", vbInformation, vbNullString
        Exit Sub
    End If
    
    'Source has been selected
    If Not Intersect(Target, Me.Range("F4")) Is Nothing Then
        On Error Resume Next
        Target.Interior.Color = gCOULEUR_SAISIE
        On Error GoTo 0
        Me.Range("N:Y").EntireColumn.Hidden = True '2025-07-25 @ 08:27
    End If

    'Date has been selected
    If Not Intersect(Target, Me.Range("K4")) Is Nothing Then
        On Error Resume Next
        Target.Interior.Color = gCOULEUR_SAISIE
        On Error GoTo 0
    End If

    'Description has been selected
    If Not Intersect(Target, Me.Range("F6")) Is Nothing Then
        If Me.Range("F4").Value = "Dépôt de client" Then
            MsgBox "Vous devez maintenant saisir le nom du client" & _
                   vbNewLine & vbNewLine & "qui effectue le dépôt de client (perçu d'avance)", _
                   vbOKOnly, "Enregistrement d'un dépôt de client"
        End If
        Target.Interior.Color = gCOULEUR_SAISIE
        Me.Range("$E$9:$K$23").Locked = False
    End If

    'Compte has been selected
    If Not Intersect(Target, Me.Range("E9:E23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
        If Target.row = 9 And Me.Range("F4").Value = "Remise TPS/TVQ" Then
            Call modGL_EJ.ConstruireEcriturePourRemiseTpsTvq(Target.row)
        End If
    End If
    
    'Debit amount has been selected
    If Not Intersect(Target, Me.Range("H9:H23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
    End If
    
    'Credit amount has been selected
    If Not Intersect(Target, Me.Range("I9:I23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
    End If
    
    'Extra description has been selected
    If Not Intersect(Target, Me.Range("J9:J23")) Is Nothing Then
        Target.Interior.Color = gCOULEUR_SAISIE
    End If
    
    'Save the current cell Address
    gPreviousCellAddress = Target.Address
    
    ActiveSheet.Protect UserInterfaceOnly:=True
    
'    'Passe à la cellule suivante
'    Application.EnableEvents = False
'    If target.Address = "$F$4" Then
'        Me.Range("K4").Select
'    End If
'     If target.Address = "$K$4" Then
'        Me.Range("F6").Select
'    End If
'    Application.EnableEvents = True
'

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim codeClient As Variant
    Dim fullDate As Variant
    Dim plageClients As Range
    
    'Si la cellule de la SOURCE change, alors on vérifie si l'on essaie...
    '   E/J récurrente
    '   Renverser une écriture
    '   Dépôt de client
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        Application.EnableEvents = False
            If UCase$(Trim$(Range("F4").Value)) = "AUTO" Then
                wshGL_EJ.Range("B2").Value = -1
                ufListeEJAuto.show
                If wshGL_EJ.Range("B2").Value >= 0 Then
                    wshGL_EJ.Range("K4").Activate
                    wshGL_EJ.Range("K4").Select
                Else
                    Call modGL_EJ.EffacerCellulesEJ
                End If
            End If
        Application.EnableEvents = True
        If Target.Value = "Renversement" Then
            Call modGL_EJ.AfficherEntreeJournalARenverser
            Exit Sub
        End If
        If Target.Value = "Dépôt de client" Then
            Call modGL_EJ.ValiderNomClientPourDepotClient
        End If
    End If
    
    'Description -OU- Nom du client a changé
    If Not Intersect(Target, Range("F6")) Is Nothing Then
        If wshGL_EJ.Range("F4").Value = "Dépôt de client" Then
            Application.EnableEvents = False
            Dim allCols As Variant
            allCols = Fn_ObtenirLigneDeFeuille("BD_Clients", Target.Value, fClntFMNomClientPlusNomClientSystème)
            'Vérifier les résultats
            If IsArray(allCols) Then
                Application.EnableEvents = False
                Target.Value = allCols(1) 'Ramener le nom du client - Colonne 1
                Application.EnableEvents = True
            Else
                MsgBox "Valeur non trouvée !!!", vbCritical
                Exit Sub
            End If
            Application.EnableEvents = False
            Dim clientCode As String
            clientCode = allCols(fClntFMClientID)
            Me.Range("B6").Value = clientCode
            Application.EnableEvents = True
            End If
    End If
    
    'Si la cellule de la DATE change, alors on essaie de construire une date
    If Not Intersect(Target, Range("K4")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_CompleteLaDate(Target.text, 999, 15)
        
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            Target.Value = Format$(fullDate, wsdADMIN.Range("USER_DATE_FORMAT").Value)
        Else
            Call modDataValidation.AfficherMessageDateInvalide("wshGL_EJ_123")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
        
        If CDate(Me.Range("K4").Value) > Date Then
            If MsgBox("En êtes-vous CERTAIN ?", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Target.Value = vbNullString
                Target.Activate
                Target.Select
            End If
        End If
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, sauvegarde du no de compte & suggestion du montant résiduel
    If Not Intersect(Target, Range("E9:G23")) Is Nothing Then
        Application.EnableEvents = False
        With wshGL_EJ
            If .Range("E" & Target.row).Value <> vbNullString Then
                .Range("L" & Target.row).Value = Fn_GetGL_Code_From_GL_Description(.Range("E" & Target.row).Value)
            End If

            If Target.row > 9 And wshGL_EJ.Range("F4").Value <> "Remise TPS/TVQ" And _
                (.Range("H26").Value <> 0 Or .Range("I26").Value <> 0) And _
                .Range("H26").Value <> .Range("I26").Value Then
                    If .Range("H26").Value > .Range("I26").Value Then
                        .Range("I" & Target.row).Value = .Range("H26").Value - .Range("I26").Value
                        Application.EnableEvents = True
                        .Range("I" & Target.row).Select
                        Application.EnableEvents = False
                    Else
                        .Range("H" & Target.row).Value = .Range("I26").Value - .Range("H26").Value
                        Application.EnableEvents = True
                        .Range("H" & Target.row).Select
                        Application.EnableEvents = False
                    End If
            End If
        End With
        Application.EnableEvents = True
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(Target, Range("H9:I23")) Is Nothing Then
        If (wshGL_EJ.Range("H" & Target.row).Value < 0 Or wshGL_EJ.Range("I" & Target.row).Value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H26").Value <> Range("I26").Value Then
            With Range("H26:I26").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("H26:I26").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
    End If
End Sub

Private Sub chkRecurrente_Click()

    If wshGL_EJ.chkRecurrente.Value = True Then
        wshGL_EJ.chkRecurrente.BackColor = gCOULEUR_SAISIE
    Else
        wshGL_EJ.chkRecurrente.BackColor = RGB(217, 217, 217)
    End If

End Sub

