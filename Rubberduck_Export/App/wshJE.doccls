Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)

    'Si la cellule de la source change, alors on vérifie si l'on essaie d'appeler des E/J récurrentes
    If Not Intersect(Target, Range("E4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        Application.EnableEvents = False
        Dim Source As String
        Source = UCase(Trim(Range("E4").value))
        If Source = "AUTO" Then
            Call UpdateJEAuto
            wshJE.Range("B2").value = -1
            usfListeEJAuto.show vbModal
            If wshJE.Range("B2").value >= 0 Then
                wshJE.Range("J4").Activate
            Else
                Call wshJEClearAllCells
                wshJE.Range("E4").Activate 'Nothing was selected
            End If
        End If
        Application.EnableEvents = True
    End If
    
    'Si la cellule de la date change, alors on essaie de construire une date
    If Not Intersect(Target, Range("J4")) Is Nothing Then
        Application.EnableEvents = False
        Dim r As Range: Set r = Range("J4")
        Call BuildDate(r)
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, sauvegarde du no de compte & suggestion du montant résiduel
    If Not Intersect(Target, Range("D9:F23")) Is Nothing Then
        Application.EnableEvents = False
        With wshJE
            If .Range("D" & Target.row).value <> "" Then
                .Range("K" & Target.row).value = GetAccountNoFromDescription(.Range("D" & Target.row).value)
            End If
            
            If Target.row > 9 And _
                (.Range("G25").value <> 0 Or .Range("H25").value <> 0) And _
                .Range("G25").value <> .Range("H25").value Then
                    If .Range("G25").value > .Range("H25").value Then
                        .Range("H" & Target.row).value = .Range("G25").value - .Range("H25").value
                        .Range("H" & Target.row).Select
                    Else
                        .Range("G" & Target.row).value = .Range("H25").value - .Range("G25").value
                        .Range("G" & Target.row).Select
                    End If
            End If
        End With
        Application.EnableEvents = True
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(Target, Range("G9:H23")) Is Nothing Then
        If (wshJE.Range("G" & Target.row).value < 0 Or wshJE.Range("H" & Target.row).value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H25").value <> Range("G25").value Then
            With Range("G25:H25").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("G25:H25").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
    End If
End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range) 'To force TAB order

    Debug.Print Target.Address
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        wshJE.Range("J4").Activate
    End If
    
    If Not Intersect(Target, Range("K4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        wshJE.Range("E6").Activate
    End If
    
    If Not Intersect(Target, Range("K6")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        wshJE.Range("D9").Activate
    End If
    
    If Not Intersect(Target, Range("K9:K23")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        Debug.Print Target.row, Target.Column
        If Target.row < 23 Then wshJE.Range("D" & Target.row + 1).Activate
    End If

End Sub
