Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)

    'Si la cellule de la source change, alors on vérifie si l'on essaie d'appeler des E/J récurrentes
    If Not Intersect(Target, Range("E4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        Application.EnableEvents = False
        Dim source As String
        source = UCase(Trim(Range("E4").value))
        If source = "AUTO" Then
            Call UpdateJEAuto
            wshJE.Range("B2").value = -1
            ufListeEJAuto.show vbModal
            If wshJE.Range("B2").value >= 0 Then
                wshJE.Range("J4").Activate
            Else
                Call wshJE_Clear_All_Cells
                wshJE.Range("E4").Activate 'Nothing was selected
            End If
        End If
        Application.EnableEvents = True
    End If
    
    'Si la cellule de la date change, alors on essaie de construire une date
    If Not Intersect(Target, Range("J4")) Is Nothing Then
        Application.EnableEvents = False
        Dim r As Range: Set r = Range("J4")
        Call Build_Date(r)
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, sauvegarde du no de compte & suggestion du montant résiduel
    If Not Intersect(Target, Range("D9:F23")) Is Nothing Then
        Application.EnableEvents = False
        With wshJE
            If .Range("D" & Target.row).value <> "" Then
                .Range("K" & Target.row).value = Get_GL_Code_From_GL_Description(.Range("D" & Target.row).value)
            End If
            
            If Target.row > 9 And _
                (.Range("G25").value <> 0 Or .Range("H25").value <> 0) And _
                .Range("G25").value <> .Range("H25").value Then
                    If .Range("G25").value > .Range("H25").value Then
                        .Range("H" & Target.row).value = .Range("G25").value - .Range("H25").value
                        .Range("H" & Target.row).Select
                    Else
                        .Range("G" & Target.row).value = .Range("H25").value - .Range("G25").value
                        .Range("G" & Target.row).Select
                    End If
            End If
        End With
        Application.EnableEvents = True
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(Target, Range("G9:H23")) Is Nothing Then
        If (wshJE.Range("G" & Target.row).value < 0 Or wshJE.Range("H" & Target.row).value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H25").value <> Range("G25").value Then
            With Range("G25:H25").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("G25:H25").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
    End If
    
    'Free up memory - 2024-02-23
    Set r = Nothing
    
End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range) '2024-02-22 - Modified to control TAB order

    Dim TabOrder As Variant, X As Variant
    Dim addr As String
    Dim rg As Range, targ As Range
    
    If TabOrderFlag = True Then Exit Sub
    ', "G9:G23", "H9:H23", "I9:I23"
    TabOrder = Array("E4", "J4", "E6", "D9:J23") 'List your cell addresses in desired tab order here
    For Each X In TabOrder
        If rg Is Nothing Then
            Set rg = Range(X)
        Else
            Set rg = Union(rg, Range(X))
        End If
    Next
    
    Set targ = Intersect(rg, Target)
    rg.Select
    If targ Is Nothing Then
        addr = Target.Cells(1, 1).Address(ColumnAbsolute:=False, RowAbsolute:=False)
        X = Application.Match(addr, TabOrder, 0)
        If IsError(X) Then Range(TabOrder(LBound(TabOrder))).Activate
    Else
        targ.Activate
    End If
    
    If Not Intersect(Target, Range("K9:K23")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
        If Target.row < 23 Then wshJE.Range("D" & Target.row + 1).Activate
    End If
    
    'Free up memory - 2024-02-23
    Set rg = Nothing

End Sub

