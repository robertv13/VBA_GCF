Option Explicit

Private Sub Worksheet_Activate() '2024-03-03 @ 10:14

    Call GL_Trans_Import_All
    Call GL_JE_Auto_Import_All
    Call wshJE_Clear_All_Cells
    wshJE.Range("K4").value = Format(Now(), "dd-mm-yyyy")

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    'Si la cellule de la SOURCE change, alors on vérifie si l'on essaie d'appeler des E/J récurrentes
    If Not Intersect(Target, Range("F4")) Is Nothing Then
        If Target.CountLarge > 1 Then Exit Sub
'        Application.EnableEvents = False
        Dim source As String
        source = UCase(Trim(Range("F4").value))
        If source = "AUTO" Then
'            Call UpdateJEAuto
            wshJE.Range("B2").value = -1
            ufListeEJAuto.show
            If wshJE.Range("B2").value >= 0 Then
                wshJE.Range("K4").Activate
                wshJE.Range("K4").Select
            Else
                Call wshJE_Clear_All_Cells
'                wshJE.Range("F4").Activate 'Nothing was selected
'                wshJE.Range("F4").Select
            End If
        End If
'        Application.EnableEvents = True
    End If
    
    'Si la cellule de la DATE change, alors on essaie de construire une date
    If Not Intersect(Target, Range("K4")) Is Nothing Then
        Application.EnableEvents = False
        Dim rng As Range: Set rng = Me.Range("K4")
        If rng.value = "" Then
            rng.value = Format(Now(), "dd-mm-yyyy")
        End If
        rng.value = Validate_A_Date(rng.value)
        If rng.value = "" Then
            Call Invalid_Date_Message
            rng.Activate
            rng.Select
        End If
        If CDate(rng.value) > Now() Then
            If MsgBox("En êtes-vous CERTAIN ?", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                rng.value = ""
                Call Pause_Application(1)
                rng.Activate
                rng.Select
                Application.EnableEvents = True
            End If
        End If
        Set rng = Nothing
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, sauvegarde du no de compte & suggestion du montant résiduel
    If Not Intersect(Target, Range("E9:G23")) Is Nothing Then
        Application.EnableEvents = False
        With wshJE
            If .Range("E" & Target.row).value <> "" Then
                .Range("L" & Target.row).value = Get_GL_Code_From_GL_Description(.Range("E" & Target.row).value)
            End If
            
            If Target.row > 9 And _
                (.Range("H26").value <> 0 Or .Range("I26").value <> 0) And _
                .Range("H26").value <> .Range("I26").value Then
                    If .Range("H26").value > .Range("I26").value Then
                        .Range("I" & Target.row).value = .Range("H26").value - .Range("I26").value
                        .Range("I" & Target.row).Select
                    Else
                        .Range("H" & Target.row).value = .Range("I26").value - .Range("H26").value
                        .Range("H" & Target.row).Select
                    End If
            End If
        End With
        Application.EnableEvents = True
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(Target, Range("H9:I23")) Is Nothing Then
        If (wshJE.Range("H" & Target.row).value < 0 Or wshJE.Range("I" & Target.row).value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H26").value <> Range("I26").value Then
            With Range("H26:I26").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("H26:I26").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
    End If
    
End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If Not Intersect(Target, Me.Range("E3:L24")) Is Nothing Then
        Dim tabOrderListOfCells As Variant, cellsAddress As Variant
        Dim addr As String
        Dim rng As Range, targetRng As Range
        
        If isTab_Order_Activated = True Then Exit Sub
        
        tabOrderListOfCells = Array("F4", "K4", "F6", _
            "E9:K9", "E10:K10", "E11:K11", "E12:K12", "E13:K13", _
            "E14:K14", "E15:K15", "E16:K16", "E17:K17", "E18:K18", _
            "E19:K19", "E20:K20", "E21:K21", "E22:K22", "E23:K23")
        For Each cellsAddress In tabOrderListOfCells
            If rng Is Nothing Then
                Set rng = Range(cellsAddress)
            Else
                Set rng = Union(rng, Range(cellsAddress))
            End If
        Next
        
        Set targetRng = Intersect(rng, Target)
        rng.Select
        If targetRng Is Nothing Then
            addr = Target.Cells(1, 1).Address(ColumnAbsolute:=False, RowAbsolute:=False)
            cellsAddress = Application.Match(addr, tabOrderListOfCells, 0)
            If IsError(cellsAddress) Then Range(tabOrderListOfCells(LBound(tabOrderListOfCells))).Activate
        Else
            targetRng.Activate
        End If
    End If
    
    'Clean memory
    Set rng = Nothing
    Set targetRng = Nothing

End Sub

Private Sub ckbRecurrente_Click()

    If ckbRecurrente.value = True Then
        ckbRecurrente.BackColor = vbYellow
    Else
        ckbRecurrente.BackColor = RGB(217, 217, 217)
    End If

End Sub
