Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate()

    If fromMenu = False Then
        Debug.Print "wshENC_Saisie_Worksheet_Activate - Je ne viens pas du menu"
        Exit Sub
    End If
    
    Dim startTime As Double: startTime = Timer: Call Log_Record("wshENC_Saisie:Worksheet_Activate", 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call Client_List_Import_All
    Call FAC_Comptes_Clients_Import_All
    Call FAC_Détails_Import_All
    Call FAC_Entête_Import_All
    Call ENC_Détails_Import_All
    Call ENC_Entête_Import_All
    Call GL_Trans_Import_All
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100
    
    Me.Application.Calculation = xlCalculationAutomatic

    Dim ws As Worksheet: Set ws = ThisWorkbook.Sheets("ENC_Saisie")
    
    On Error Resume Next
    Me.columns("A:B").Hidden = True
    On Error GoTo 0
    
    'Protect the Worksheet
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    Call SetTabOrder(ws)
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    With ws
        .Range("F5").Select
        previousCellAddress = .Range("F5").Address
    End With

    'Cleaning memory - 2024-07-01 @ 09:34
    Set ws = Nothing
    
    Call Log_Record("wshDEB_Saisie:Worksheet_Activate()", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    
    If Target.CountLarge > 1 Then Exit Sub
    
    'Client has been selected
    If Not Intersect(Target, Me.Range("F5")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Date has been selected
    If Not Intersect(Target, Me.Range("K5")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Payment Type has been selected
    If Not Intersect(Target, Me.Range("F7")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Total deposit has been selected
    If Not Intersect(Target, Me.Range("K7")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'Comments has been selected
    If Not Intersect(Target, Me.Range("F9")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    
    Dim fullDate As Variant
    
    'On Customer Change BUT NOT on Payment load and only on new payments
    If Not Intersect(Target, Me.Range("F5")) Is Nothing And _
        Me.Range("F5").value <> Empty And _
            Me.Range("B4").value = False And _
            Me.Range("B6").value = "" Then
        Application.EnableEvents = False
        Dim clientCode As String
        clientCode = Fn_GetID_From_Client_Name(Me.Range("F5").value)
        Me.Range("B8").value = clientCode
        Call ENC_Load_OS_Invoices(clientCode)
        Application.EnableEvents = True
    End If
    
    'Date has changed
    If Not Intersect(Target, Me.Range("K5")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(CStr(Target.text))
        If fullDate <> "Invalid Date" Then
            Target.value = fullDate
        Else
            Call MsgBoxInvalidDate
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.GoTo Range(Target.Address)
        End If
       
        'Future date ?
        If CDate(Range("K5").value) > Format$(Now(), "dd/mm/yyyy") Then
            If MsgBox("En êtes-vous CERTAIN ?", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Application.EnableEvents = False
                Target.ClearContents
                Application.EnableEvents = True
                Application.GoTo Range(Target.Address)
            End If
        End If
        
    End If
    
    'Force TAB order after a cell has been changed
    If Not Intersect(Target, Me.Range("F5")) Is Nothing Then
        Me.Range("K5").Select
    ElseIf Not Intersect(Target, Me.Range("K5")) Is Nothing Then
        Me.Range("F7").Select
    ElseIf Not Intersect(Target, Me.Range("F7")) Is Nothing Then
        Me.Range("K7").Select
    ElseIf Not Intersect(Target, Me.Range("K7")) Is Nothing Then
        Me.Range("F9").Select
    End If

End Sub
