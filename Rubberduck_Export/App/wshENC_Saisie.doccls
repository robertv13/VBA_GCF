Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate()

    Dim timerStart As Double: timerStart = Timer: Call Start_Timer("wshENC_Saisie:Worksheet_Activate()")
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call Client_List_Import_All
    Call FAC_Comptes_Clients_Import_All
    Call FAC_Détails_Import_All
    Call FAC_Entête_Import_All
    Call ENC_Détails_Import_All
    Call ENC_Entête_Import_All
    
    'Set the zoom factor to 95% when this worksheet is activated
    ActiveWindow.Zoom = 95
    
    Me.Application.Calculation = xlCalculationAutomatic

    Dim ws As Worksheet: Set ws = ThisWorkbook.Sheets("ENC_Saisie")
    
    'Protect the Worksheet
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    Call SetTabOrder(ws)
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    With ws
        .Range("F5").Select
        previousCellAddress = .Range("F5").Address
    End With

    'Cleaning memory - 2024-07-01 @ 09:34
    Set ws = Nothing
    
    Call End_Timer("wshDEB_Saisie:Worksheet_Activate()", timerStart)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    
    If Target.CountLarge > 1 Then Exit Sub
    
    If Not Intersect(Target, Me.Range("B12:B31")) Is Nothing Then
        Debug.Print Target.Address, Target.value
    End If

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    
    'On Customer Change BUT NOT on Payment load and only on new payments
    If Not Intersect(Target, Me.Range("F5")) Is Nothing And _
        Me.Range("F5").value <> Empty And Me.Range("B4").value = False And Me.Range("B6").value = "" Then
        Application.EnableEvents = False
        Dim clientCode As String
        clientCode = Fn_GetID_From_Client_Name(Me.Range("F5").value)
        Me.Range("B8").value = clientCode
        Call ENC_Load_OS_Invoices(clientCode)
        Application.EnableEvents = True
    End If
        
    'Apply checkbox has changed ?
    'See Apply_Click Procedure (OnAction with every Checkbox)

End Sub
