'@Folder("Saisie_Encaissement")

Option Explicit

Public clientCode As String
Public pmtNo As Long
Public GLEntryNo As Long

Private Sub Worksheet_Activate() '2025-11-16 @ 15:20

'   On Error GoTo CleanFail

    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Application.Calculation = xlCalculationAutomatic
    
    If gFromMenu = False Then GoTo CleanExit
    
    Dim startTime As Double: startTime = Timer
    Call modDev_Utils.EnregistrerLogApplication("wshENC_Saisie:Worksheet_Activate", vbNullString, 0)
    
    'Étapes principales
    Call InitialiserFeuille
    Call ImporterDonnees
    Call ConfigurerInterface
    Call NettoyerBordereau
    Call ProtegerFeuilleEncaissement(Me)
    Call CreerNouvelEncaissement
    
    'Positionner le curseur sur le premier champ
    Me.Range("F5").Select
    Call HighlightCell(Me.Range("F5"))
    
CleanExit:
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    Call modDev_Utils.EnregistrerLogApplication("wshENC_Saisie:Worksheet_Activate", vbNullString, startTime)
    Exit Sub
    
CleanFail:
    MsgBox "Erreur inattendue dans Worksheet_Activate : " & Err.description, vbCritical
    Resume CleanExit

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range) '2025-11-16 @ 15:20

'   On Error GoTo CleanFail
    
    If Target.Address = "$F$6" Then
        Me.Range("K5").Select
        Exit Sub
    End If
    
    Call HighlightCell(Target)
    
    'Gestion des sélections principales
    If Not Intersect(Target, Me.Range("F5,K5,F7,K7,F9")) Is Nothing Then
        Call GererSelectionChampsPrincipaux(Target)
    End If
    
    'Première case à cocher
    If Not Intersect(Target, Me.Range("E12")) Is Nothing Then
        GererSelectionPremiereCaseACocher
    End If
    
    'Cas particulier : G7 ? rediriger vers K7
    If Not Intersect(Target, Me.Range("G7")) Is Nothing Then
        Call GererCasSpecialG7(Target)
        Exit Sub
    End If
    
    Exit Sub
    
CleanFail:
    MsgBox "Erreur inattendue dans Worksheet_SelectionChange :" & vbCrLf & vbCrLf & _
           Err.description, vbCritical, "Veuillez contacter le développeur"
           
End Sub

Private Sub Worksheet_Change(ByVal Target As Range) '2025-11-16 @ 15:40

    On Error GoTo CleanFail
    
    'Si le code est en train de modifier la feuille, ignorer l'événement
    If gIsUpdating Then Exit Sub
    
    'Désactiver les événements pour éviter la boucle
    Application.EnableEvents = False
    
    'Gestion des changements selon la cellule
    If Not Intersect(Target, Me.Range("F5")) Is Nothing Then
        gIsUpdating = True
        Call modENC_Saisie.GererChangementClient(Me, Target)
        gIsUpdating = False
        'Force le focus sur la prochaine cellule
        Me.Range("K5").Select
        
    ElseIf Not Intersect(Target, Me.Range("K5")) Is Nothing Then
        gIsUpdating = True
        Call modENC_Saisie.GererChangementDate(Me, Target)
        gIsUpdating = False
        
    ElseIf Not Intersect(Target, Me.Range("F7")) Is Nothing Then
        gIsUpdating = True
        Call modENC_Saisie.GererChangementTypeEnc(Me, Target)
        gIsUpdating = False
    End If
    
CleanExit:
    Application.EnableEvents = True
    Exit Sub

CleanFail:
    MsgBox "Erreur inattendue dans Worksheet_Change :" & vbCrLf & vbCrLf & _
           Err.description, vbCritical, "Veuillez contacter le développeur"
    gIsUpdating = False
    Resume CleanExit
    
End Sub

Private Sub GererSelectionChampsPrincipaux(ByVal Target As Range)

    Target.Interior.Color = gCOULEUR_SAISIE
    
End Sub

Private Sub GererSelectionPremiereCaseACocher()

    Call ValiderEtLancerufEncRegularisation
    
End Sub

Private Sub GererCasSpecialG7(ByVal Target As Range)

    'G7 est réservé : si sélectionné, on force le focus vers K7
    Target.Interior.Color = gCOULEUR_BASE_COMPTABILITE
    Me.Range("K7").Select
    
End Sub

