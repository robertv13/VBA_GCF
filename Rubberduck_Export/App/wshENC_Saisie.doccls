Option Explicit

Public previousCellAddress As Variant
Public clientCode As String
Public pmtNo As Long
Public GLEntryNo As Long

Private Sub Worksheet_Activate()

    If fromMenu = False Then
        Debug.Print "#007 - wshENC_Saisie_Worksheet_Activate - Je ne viens pas du menu"
        Exit Sub
    End If
    
    Dim startTime As Double: startTime = Timer: Call Log_Record("wshENC_Saisie:Worksheet_Activate", 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call Client_List_Import_All
    Call FAC_Comptes_Clients_Import_All
    Call FAC_Détails_Import_All
    Call FAC_Entête_Import_All
    Call ENC_Détails_Import_All
    Call ENC_Entête_Import_All
    Call GL_Trans_Import_All
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100
    
    Me.Application.Calculation = xlCalculationAutomatic

    Dim ws As Worksheet: Set ws = ThisWorkbook.Sheets("ENC_Saisie")
    
    On Error Resume Next
    Me.Columns("A:B").Hidden = True
    On Error GoTo 0
    
    'S'assurer que les cellules qui auraient été déverouillées le soient pour commencer
    On Error Resume Next
    ws.Range("B12:B36").Locked = True
    ws.Range("E12:E36").Locked = True
    On Error GoTo 0
    
    'Certaines cellules perdent la couleur du background
    Dim cell As Range
    For Each cell In ws.Range("I5,F6")
        With cell.Interior
            .Pattern = xlSolid
            .PatternColorIndex = xlAutomatic
            .ThemeColor = xlThemeColorAccent6
            .TintAndShade = 0.599993896298105
            .PatternTintAndShade = 0
        End With
    Next cell

    'Protect the Worksheet
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlNoRestrictions
    End With
    
    Call SetTabOrder(ws)
    
    Call ENC_Remove_Check_Boxes(36)
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    With ws
        .Range("K5").value = Format$(Now(), wshAdmin.Range("B1").value)
        .Range("F5").Select
        previousCellAddress = .Range("F5").Address
    End With

    'Libérer la mémoire
    Set cell = Nothing
    Set ws = Nothing
    
    Call Log_Record("wshDEB_Saisie:Worksheet_Activate", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    
    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = xlNone
    End If
    
    'Client has been selected
    If Not Intersect(Target, Me.Range("F5")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Date has been selected
    If Not Intersect(Target, Me.Range("K5")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Payment Type has been selected
    If Not Intersect(Target, Me.Range("F7")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Total deposit has been selected
    If Not Intersect(Target, Me.Range("K7")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'Comments has been selected
    If Not Intersect(Target, Me.Range("F9")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    previousCellAddress = Target.Address
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    
    Dim fullDate As Variant
    
    'On Customer Change and only on new payments
    If Not Intersect(Target, Me.Range("F5")) Is Nothing And _
        Me.Range("F5").value <> Empty And _
            Me.Range("B6").value = "" Then
        Application.EnableEvents = False
        clientCode = Fn_GetID_From_Client_Name(Me.Range("F5").value)
        Call ENC_Remove_Check_Boxes(36)
        Call ENC_Get_OS_Invoices(clientCode)
        Application.EnableEvents = True
    End If
    
    'Date has changed
    If Not Intersect(Target, Me.Range("K5")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(Target.Text, 30, 0)
        If fullDate <> "Invalid Date" Then
            Target.value = Format$(fullDate, wshAdmin.Range("B1").value)
        Else
            Call MsgBoxInvalidDate("wshENC_Saisie_115")
            Target.ClearContents
            Application.EnableEvents = True
            Application.GoTo Range(Target.Address)
        End If
        Application.EnableEvents = True
    End If
    
    'Force TAB order after a cell has been changed
    On Error Resume Next
    If Not Intersect(Target, Me.Range("F5")) Is Nothing Then
        Me.Range("K5").Select
'    ElseIf Not Intersect(Target, Me.Range("K5")) Is Nothing Then
'        Me.Range("F7").Select
    ElseIf Not Intersect(Target, Me.Range("F7")) Is Nothing Then
        Me.Range("K7").Select
    ElseIf Not Intersect(Target, Me.Range("K7")) Is Nothing Then
        Me.Range("F9").Select
    End If
    On Error GoTo 0

End Sub
