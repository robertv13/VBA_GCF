Option Explicit

Private Sub Workbook_BeforeClose(Cancel As Boolean) '2024-01-08 @ 20:09

    Worksheets("Menu").Activate
    HideAllExcepActiveSheet

End Sub

Private Sub Workbook_Open()
    Application.ScreenUpdating = False
    Windows(ThisWorkbook.Name).Visible = False
    'frmSplashScreen.show
    Worksheets("Menu").Activate
    Range("B1").Select
    'HideAllExcepActiveSheet
    
    Call ImportChartOfAccount
    
    Windows(ThisWorkbook.Name).Visible = True
    Application.ScreenUpdating = True
End Sub

Private Sub HideAllExcepActiveSheet()
    
    Dim wsh As Worksheet
    For Each wsh In ThisWorkbook.Worksheets
        If wsh.Name <> ActiveSheet.Name Then wsh.Visible = xlSheetHidden
    Next wsh
    
End Sub

Sub ImportChartOfAccount() '2024-01-09 @ 09:22
    
    'Clear all cells, but the headers, in the target worksheet
    wshAdmin.Range("T10").CurrentRegion.Offset(2, 0).ClearContents

    'Import Accounts List from 'GCF_BD_Entrée.xlsx, in order to always have the LATEST version
    Dim sourceWorkbook As String, sourceWorksheet As String
    sourceWorkbook = wshAdmin.Range("FolderSharedData").value & Application.PathSeparator & _
                     "GCF_BD_Entrée.xlsx"
    sourceWorksheet = "PlanComptable"
    
    'ADODB connection
    Dim connStr As ADODB.Connection
    Set connStr = New ADODB.Connection
    
    'Connection String specific to EXCEL
    connStr.ConnectionString = "Provider = Microsoft.ACE.OLEDB.12.0;" & _
                               "Data Source = " & sourceWorkbook & ";" & _
                               "Extended Properties = 'Excel 12.0 Xml; HDR = YES';"
    connStr.Open
    
    'Recordset
    Dim recSet As ADODB.Recordset
    Set recSet = New ADODB.Recordset
    
    recSet.ActiveConnection = connStr
    recSet.Source = "SELECT * FROM [" & sourceWorksheet & "$]"
    recSet.Open
    
    'Copy to wshAdmin workbook
    wshAdmin.Range("T11").CopyFromRecordset recSet
'    wshClientDB.Range("A1").CurrentRegion.EntireColumn.AutoFit
    
    'Close resource
    recSet.Close
    connStr.Close
    
    Call RedefineDynamicRange
    
    MsgBox _
        Prompt:="J'ai importé un total de " & _
            Format(wshAdmin.Range("T10").CurrentRegion.Rows.count - 1, _
            "## ##0") & " comptes du Grand Livre", _
        Title:="Vérification du nombre de comptes", _
        Buttons:=vbInformation
        
End Sub

Sub RedefineDynamicRange() 'Redefine DynamicNameRange - 2024-01-09 @ 10:21
    
    'Delete existing dynamic named range (assuming it exists)
    On Error Resume Next
    ThisWorkbook.Names("dnrPlanComptable").Delete
    On Error GoTo 0
    
    'Define a new dynamic named range
    Dim newRangeFormula As String
    newRangeFormula = "=OFFSET(Admin!$T$11,,,COUNTA(Admin!$T:$T)-2,4)"
    
    'Create a new dynamic named range
    ThisWorkbook.Names.Add Name:="dnrPlanComptable", RefersTo:=newRangeFormula
    
End Sub
