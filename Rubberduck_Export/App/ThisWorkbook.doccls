Option Explicit

Private Sub Workbook_Open()

    Dim tt0 As Double: tt0 = Timer
    
    If Not Application.Visible Then
        Call EnregistrerErreurs("ThisWorkbook", "Workbook_Open", "Excel est en mode INVISIBLE", _
                                                                                Err.Number, "ERREUR")
        MsgBox "Ouverture non autorisée en mode invisible.", _
        vbCritical, _
        "Vérification au démarrage (ThisWorkbook_Open)"
        ThisWorkbook.Close SaveChanges:=False
    End If
    
    If Application.EnableEvents = False Then
        Call EnregistrerErreurs("ThisWorkBook", "Open", "Démarrage ANORMAL", Err.Number, "ERREUR")
        MsgBox "Les événements Excel sont désactivés." & vbCrLf & vbCrLf & _
            "L'application ne peut pas démarrer correctement.", _
            vbCritical
        Exit Sub
    End If

    If Application.StartupPath = Application.path Then
        MsgBox "Veuillez ouvrir l'application via le raccourci prévu, et non via Excel.", vbExclamation
        ThisWorkbook.Close SaveChanges:=False
    End If
    
    gSessionInitialisee = True
    
    Dim utilisateurWindows As String
    utilisateurWindows = modFunctions.Fn_UtilisateurWindows
    
    Call modTraceSession.CreerTraceOuverture '2025-11-10 @ 08:38
    
'    Call InitialiserMenuPrincipal("Workbook_Open") '2025-11-10 @ 08:52
    
    'Chemin du dossier contenant les fichiers PROD - 2025-08-12 @ 05:51
    Dim cheminSourcePROD As String
    cheminSourcePROD = Fn_RepertoireBaseApplication(utilisateurWindows)
    
    If utilisateurWindows <> "RobertMV" And utilisateurWindows <> "Robertmv" Then
        If Dir(cheminSourcePROD & "\GCF_BD_MASTER.lock") <> vbNullString Then
            MsgBox "Cette application est actuellement en maintenance." & vbNewLine & vbNewLine & _
                   "Le fichier principal est verrouillé par le développeur." & vbNewLine & vbNewLine & _
                   "Veuillez ressayer dans 5 à 10 minutes SVP", _
                   vbCritical, _
                   "L'application APP n'est pas disponible"
            Call modMenu.FermerApplication("Application est verrouillée", True)
        End If
    End If

    Dim versionApplication As String
    versionApplication = Fn_ExtraireVersionDepuisNomClasseur
    
    Call modSurveillance.LancerSurveillance '2025-11-06 @ 15:56
    
    gDerniereActiviteNomFeuille = ActiveSheet.Name
    gDerniereActiviteAdresseSelection = Selection.Address
    gDerniereInteractionTimer = Timer
    
    Call modAppli.DemarrerApplication(utilisateurWindows)

    'Message de bienvenue
    MsgBox "Bienvenue " & UtilisateurActif("Prenom") & " !" & vbNewLine & vbNewLine & _
           "Version de l'application : " & versionApplication, _
           vbInformation, _
           "Démarrage de l'application GCF"

    Call VerifierVersionApplication(cheminSourcePROD, versionApplication)
    
    Call EnregistrerLogPerformance(vbNullString, -1)
    Call EnregistrerLogPerformance("Ouverture de session", Timer - tt0)
    
End Sub
    
Private Sub Workbook_Activate() '2025-11-10 @ 11:15

    Call modDev_Utils.EnregistrerLogApplication("ThisWorkbook.Workbook_Activate déclenché", ThisWorkbook.Name, 0)
    
End Sub

Private Sub Workbook_Deactivate()  '2025-11-10 @ 11:15

    Call modDev_Utils.EnregistrerLogApplication("ThisWorkbook.Workbook_Deactivate déclenché", ThisWorkbook.Name, 0)
    
End Sub

Private Sub Workbook_SheetActivate(ByVal Sh As Object) '2025-07-03 @ 10:58

    Dim nomFeuilleFacture As String: nomFeuilleFacture = "FAC_Finale" '2025-07-19 @ 18:46
    Dim statutFacture As String: statutFacture = Trim(ThisWorkbook.Names("FactureStatut").RefersToRange.Value)

    If statutFacture = "En attente de mise à jour" Then
        If Sh.Name <> nomFeuilleFacture And Sh.Name <> "FAC_Finale_Intact" Then
            Debug.Print "777 - " & Sh.Name
            Application.EnableEvents = False
            MsgBox "Veuillez d'abord SAUVEGARDER la facture actuelle" & vbNewLine & vbNewLine & _
                    "avant de quitter la facture FINALE", vbExclamation, _
                    "Contrôle imposé par l'application (ThisWorkbook)"
            Sheets(nomFeuilleFacture).Activate
            Application.EnableEvents = True
        End If
    End If
    
    gDerniereActiviteNomFeuille = Sh.Name '2025-11-06 @ 16:15
    gDerniereInteractionTimer = Timer
    
    Call modDev_Utils.EnregistrerLogApplication("ThisWorkbook.Workbook_SheetActivate déclenché", Sh.Name, 0)

End Sub

Private Sub Workbook_BeforeClose(Cancel As Boolean)
    
    If gFermetureForcee Then
        Cancel = False 'Autoriser la fermeture réelle
    Else
        Cancel = True
        ufExitDisable.show
        If ufExitDisable.Visible Then Unload ufExitDisable
    End If
    
End Sub

Private Sub Workbook_SheetDeactivate(ByVal Sh As Object)
    
    Call modDev_Utils.EnregistrerLogApplication("ThisWorkbook.Workbook_SheetDeactivate déclenché", Sh.Name, 0)

End Sub

Private Sub Workbook_SheetSelectionChange(ByVal Sh As Object, ByVal Target As Range) '2025-11-06 @ 16:16
    
    gDerniereActiviteAdresseSelection = Target.Address
    gDerniereInteractionTimer = Timer

End Sub