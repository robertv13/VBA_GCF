Option Explicit

Private Sub Workbook_Open()

    'Chemin du dossier contenant les fichiers PROD - 2025-08-12 @ 05:51
    Dim cheminSourcePROD As String
    cheminSourcePROD = "P:\Administration\APP\GCF\DataFiles\"

    If Fn_UtilisateurWindows <> "RobertMV" And Fn_UtilisateurWindows <> "Robertmv" Then
        If Dir(cheminSourcePROD & "\GCF_BD_MASTER.lock") <> vbNullString Then
            MsgBox "Cette application est actuellement en maintenance." & vbNewLine & vbNewLine & _
                   "Le fichier principal est verrouillé par le développeur." & vbNewLine & vbNewLine & _
                   "Veuillez ressayer dans 5 à 10 minutes SVP", _
                   vbCritical, _
                   "L'application APP n'est pas disponible"
            Call modMenu.FermerApplicationNormalement(modFunctions.Fn_NomUtilisateurWindows())
        End If
    End If

    'Message de bienvenue
    MsgBox "Bienvenue " & Fn_UtilisateurWindows & " !" & vbNewLine & vbNewLine & _
           "Version de l'application : " & gVERSION_APPLICATION, _
           vbInformation, _
           "Démarrage de l'application GCF"

    'Contrôle de version
    Dim versionMinimale As String
    versionMinimale = "7.B.07"

    If modAppli.Fn_VersionCompare(gVERSION_APPLICATION, versionMinimale) < 0 Then
        MsgBox "Votre version (" & gVERSION_APPLICATION & ") est obsolète." & vbNewLine & vbNewLine & _
               "Veuillez utiliser la dernière version (" & versionMinimale & ") sur le portail.", _
               vbCritical, _
               "Version de l'application est non supportée"
        Call modMenu.FermerApplicationNormalement(Fn_UtilisateurWindows)
        Exit Sub
    End If
    
    gDerniereActivite = Now
    gProchaineVerification = Now + TimeSerial(0, gFREQUENCE_VERIFICATION_INACTIVITE, 0)

    Application.OnTime gProchaineVerification, "VerifierDerniereActivite"

    Call modAppli.DemarrerApplication

End Sub
    
Private Sub Workbook_SheetActivate(ByVal Sh As Object) '2025-07-03 @ 10:58

    Dim nomFeuilleFacture As String: nomFeuilleFacture = "FAC_Finale" '2025-07-19 @ 18:46
    Dim statutFacture As String: statutFacture = Trim(ThisWorkbook.Names("FactureStatut").RefersToRange.Value)

    If statutFacture = "En attente de mise à jour" Then
        If Sh.Name <> nomFeuilleFacture Then
            Application.EnableEvents = False
            MsgBox "Veuillez d'abord finaliser la facture (Sauvegarde)" & vbNewLine & vbNewLine & _
                    "avant de quitter la facture FINALE", vbExclamation, _
                    "Contrôle imposé par l'application"
            Sheets(nomFeuilleFacture).Activate
            Application.EnableEvents = True
        End If
    End If
    
    Call modAppli.EnregistrerActivite("SheetActivate [" & Sh.Name & "]")
    
End Sub

Private Sub Workbook_SheetDeactivate(ByVal Sh As Object) '2025-07-03 @ 10:58

    Call modAppli.EnregistrerActivite("SheetDeactivate [" & Sh.Name & "]")
    
End Sub

Private Sub Workbook_BeforeClose(Cancel As Boolean)
    
    'La sortie du classeur ne peut se faire par le X rouge à droite en haut de EXCEL
    Cancel = True
    ufExitDisable.show

End Sub