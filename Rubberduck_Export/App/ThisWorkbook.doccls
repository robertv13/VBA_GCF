Option Explicit

Private Sub Workbook_Open()

    Dim utilisateurWindows As String
    utilisateurWindows = modFunctions.Fn_UtilisateurWindows
    'Chemin du dossier contenant les fichiers PROD - 2025-08-12 @ 05:51
    
    Dim cheminSourcePROD As String
    cheminSourcePROD = Fn_RepertoireBaseApplication(utilisateurWindows)
    
    If utilisateurWindows <> "RobertMV" And utilisateurWindows <> "Robertmv" Then
        If Dir(cheminSourcePROD & "\GCF_BD_MASTER.lock") <> vbNullString Then
            MsgBox "Cette application est actuellement en maintenance." & vbNewLine & vbNewLine & _
                   "Le fichier principal est verrouillé par le développeur." & vbNewLine & vbNewLine & _
                   "Veuillez ressayer dans 5 à 10 minutes SVP", _
                   vbCritical, _
                   "L'application APP n'est pas disponible"
            Call modMenu.FermerApplicationNormalement(utilisateurWindows)
        End If
    End If

    Dim versionApplication As String
    versionApplication = Fn_ExtraireVersionDepuisNomClasseur
    
    gDerniereActivite = Now
    gProchaineVerification = Now + TimeSerial(0, gFREQUENCE_VERIFICATION_INACTIVITE, 0)

    Application.OnTime gProchaineVerification, "VerifierDerniereActivite"

    Call modAppli.DemarrerApplication(utilisateurWindows)

    'Message de bienvenue
    MsgBox "Bienvenue " & UtilisateurActif("Prenom") & " !" & vbNewLine & vbNewLine & _
           "Version de l'application : " & versionApplication, _
           vbInformation, _
           "Démarrage de l'application GCF"

    Call VerifierVersionApplication(cheminSourcePROD, versionApplication)
    
End Sub
    
Private Sub Workbook_SheetActivate(ByVal sh As Object) '2025-07-03 @ 10:58

    Dim nomFeuilleFacture As String: nomFeuilleFacture = "FAC_Finale" '2025-07-19 @ 18:46
    Dim statutFacture As String: statutFacture = Trim(ThisWorkbook.Names("FactureStatut").RefersToRange.Value)

    If statutFacture = "En attente de mise à jour" Then
        If sh.Name <> nomFeuilleFacture And sh.Name <> "FAC_Finale_Intact" Then
            Debug.Print "777 - " & sh.Name
            Application.EnableEvents = False
            MsgBox "Veuillez d'abord SAUVEGARDER la facture actuelle" & vbNewLine & vbNewLine & _
                    "avant de quitter la facture FINALE", vbExclamation, _
                    "Contrôle imposé par l'application (ThisWorkbook)"
            Sheets(nomFeuilleFacture).Activate
            Application.EnableEvents = True
        End If
    End If
    
    Call modAppli.EnregistrerActivite("SheetActivate [" & sh.Name & "]")
    
End Sub

Private Sub Workbook_SheetDeactivate(ByVal sh As Object) '2025-07-03 @ 10:58

    Call modAppli.EnregistrerActivite("SheetDeactivate [" & sh.Name & "]")
    
End Sub

Private Sub Workbook_BeforeClose(Cancel As Boolean)
    
    'La sortie du classeur ne peut se faire par le X rouge à droite en haut de EXCEL
    Cancel = True
    ufExitDisable.show

End Sub