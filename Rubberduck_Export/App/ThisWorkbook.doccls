Option Explicit

Public startTime As Double
Public previousCellAddress As Variant

Private Sub Workbook_Open()

    Dim rootPath As String
    Call Set_Root_Path(rootPath)
    wshAdmin.Range("F5").value = rootPath

    'Vérification si le chemin est accessible - Premier Essai
    If Not Fn_Chemin_Existe(rootPath & "\") Then
        MsgBox "Le chemin réseau est inaccessible." & vbNewLine & vbNewLine & _
               "Veuillez vérifier votre connexion au serveur SVP", vbCritical, "Incapacité d'accéder au serveur (1er essai)"
        Exit Sub
    End If

    'Le réseau est disponible, reste à vérifier si l'affectation a eu lieu...
    If wshAdmin.Range("F5").value = "" Then
        MsgBox "L'adresse du serveur n'est pas valide !!!", vbCritical, "Accès au serveur est impossible"
    End If
    
    'Log activity
    Dim startTime As Double: startTime = Timer: Call Log_Record("***** Début d'une nouvelle session (Workbook_Open) *****", 0)
    
    Application.ScreenUpdating = False
    
'    Call Set_Environment
    
    'Vérification si le chemin est accessible - Deuxième essai
    If Not Fn_Chemin_Existe(rootPath & "\") Then
        MsgBox "Le chemin réseau est inaccessible (deuxième essai)." & vbNewLine & vbNewLine & _
               "Veuillez vérifier votre connexion au serveur SVP", vbCritical, _
               "Incapacité d'accéder au serveur (2ème essai)"
        Exit Sub
    End If
    
    'Call the BackupMasterFile (GCF_BD_MASTER.xlsx) macro at each application startup
    Call BackupMasterFile
    
    Call Hide_Dev_Shapes_Based_On_Username
    
    Application.ScreenUpdating = True
    
    Call Write_Info_On_Main_Menu
    
    Application.ScreenUpdating = False
    
    wshMenu.Activate
    wshMenu.Range("A1").value = wshAdmin.Range("NomEntreprise").value
    
'    'Protéger les structures du classeur avec un mot de passe
'    ThisWorkbook.Protect Password:="pmdpf", Structure:=True

    'Protect wshMenu
    With wshMenu
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells '2024-08-21 @06:33
    End With
    
    Call Slide_In_All_Menu_Options

    Dim wb As Workbook: Set wb = ActiveWorkbook
    wb.Application.Calculation = xlCalculationManual
   
    Application.ScreenUpdating = True

    'Cleaning memory - 2024-07-01 @ 09:34
    Set wb = Nothing
    
    Call Log_Record("ThisWorkbook:Workbook_Open()", startTime)
    
End Sub

Private Sub Workbook_BeforeClose(Cancel As Boolean)
    
    Cancel = True
    ufExitDisable.show

End Sub

'Fonction pour vérifier si un fichier ou un dossier existe
Private Function Fn_Chemin_Existe(ByVal chemin As String) As Boolean

    On Error Resume Next
    Fn_Chemin_Existe = (Dir(chemin) <> "")
    On Error GoTo 0
    
End Function

Sub BackupMasterFile()

    Dim startTime As Double: startTime = Timer: Call Log_Record("modAppli:BackupMasterFile", 0)
    
    Application.ScreenUpdating = False
    
    'Open the master file
    Dim masterWorkbook As Workbook
    Set masterWorkbook = Workbooks.Open(wshAdmin.Range("F5").value & DATA_PATH & Application.PathSeparator & _
                            "GCF_BD_MASTER.xlsx")
    
    'Get the current date and time in the format YYYYMMDD_HHMMSS
    Dim currentDateAndTime As String
    currentDateAndTime = Format$(Now, "YYYYMMDD_HHMMSS")

    'Create the backup file name
    Dim backupFileName As String
    backupFileName = Left(masterWorkbook.name, InStrRev(masterWorkbook.name, ".") - 1) & "_" & currentDateAndTime & ".xlsx"

    'Define the backup file path (same directory as the master file)
    Dim backupFilePath As String
    backupFilePath = masterWorkbook.path & "\" & backupFileName

    'Save a copy of the master workbook with the new name
    masterWorkbook.SaveCopyAs backupFilePath

    'Close the master workbook
    masterWorkbook.Close saveChanges:=False

'    'Optional: Notify the user
'    MsgBox "Backup created: " & vbNewLine & vbNewLine & "'" & backupFilePath & "'"

    Application.ScreenUpdating = True

    'Cleaning memory - 2024-07-01 @ 09:34
    Set masterWorkbook = Nothing
    
    Call Log_Record("modAppli:BackupMasterFile()", startTime)

End Sub