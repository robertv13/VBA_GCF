Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate() '2024-09-01 @ 04:52

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshCAR_Liste_Agée:Worksheet_Activate", "", 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    Dim ws As Worksheet: Set ws = wshCAR_Liste_Agée
    
    'Import transactions from MASTER file
    Call Client_List_Import_All
    Call FAC_Entête_Import_All
    Call FAC_Comptes_Clients_Import_All
    Call ENC_Détails_Import_All
    Call ENC_Entête_Import_All
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100

    Me.Application.Calculation = xlCalculationAutomatic

    ws.Unprotect
    
    Application.EnableEvents = False
    
    wshCAR_Liste_Agée.Range("B4").Value = "Facture"
    wshCAR_Liste_Agée.Range("D4").Value = "Numéro de facture"
    wshCAR_Liste_Agée.Range("F4").Value = "Non"
    wshCAR_Liste_Agée.Range("H4").Value = Format$(Date, wshAdmin.Range("B1").Value)
    
    Call SetTabOrder(ws)
    
    Application.ScreenUpdating = True
    
    'Efface la liste précédente
    Dim lastUsedRow As Long
    lastUsedRow = ws.Cells(ws.Rows.count, "B").End(xlUp).row
    If lastUsedRow > 7 Then
        ws.Range("B8:J" & lastUsedRow + 2).Clear
    End If
    
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    Application.ScreenUpdating = True
    
    ws.Range("B4").Select
    previousCellAddress = ws.Range("B4").Address

    'Libérer la mémoire
    Set ws = Nothing
    
    Call Log_Record("wshCAR_Liste_Agée:Worksheet_Activate", "", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshCAR_Liste_Agée:Worksheet_SelectionChange", Target.Address, 0)
    
    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = COULEUR_BASE_FACTURATION
    End If
    
    'Is Niveau de Détail been selected ?
    If Not Intersect(Target, wshCAR_Liste_Agée.Range("B4")) Is Nothing Then
        Target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Is Ordre de tri been selected ?
    If Not Intersect(Target, wshCAR_Liste_Agée.Range("D4")) Is Nothing Then
        If wshCAR_Liste_Agée.Range("B4").Value = "Client" Then
            wshCAR_Liste_Agée.Range("D4").Value = "Nom de client"
        End If
        Target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Is Solde à 0,00 $ been selected ?
    If Not Intersect(Target, wshCAR_Liste_Agée.Range("F4")) Is Nothing Then
        Target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Is Date Limite been selected ?
    If Not Intersect(Target, wshCAR_Liste_Agée.Range("H4")) Is Nothing Then
        Target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Save the current cell Address
    previousCellAddress = Target.Address
    
    Call Log_Record("wshCAR_Liste_Agée:Worksheet_SelectionChange", "", startTime)
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshCAR_Liste_Agée:Worksheet_Change", "", 0)
    
    Dim fullDate As Variant
    
    'Critère de tri has changed
    If Not Intersect(Target, Range("D4")) Is Nothing Then
        If Range("B4").Value = "Client" Then
            If Target.Value = "Numéro de facture" Then
                Target.Value = "Nom de client"
            End If
        End If
    End If
    
    'CutOffDate has changed
    If Not Intersect(Target, Range("H4")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(Target.Text, 365, 0)
        If fullDate <> "Invalid Date" Then
            Target.Value = Format$(fullDate, wshAdmin.Range("B1").Value)
            Application.EnableEvents = True
        Else
            Call modTEC_Saisie.AfficherMessageDateInvalide("wshCAR_Liste_Agée_125")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
       
        'Future date ?
        If CDate(Range("H4").Value) > Date Then
            If MsgBox("Il n'est pas permis d'utiliser une date dans le futur !", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Application.EnableEvents = False
                Target.ClearContents
                Application.EnableEvents = True
                Application.Goto Range(Target.Address)
            End If
        End If
    End If

    Application.EnableEvents = True
    
    Call Log_Record("wshCAR_Liste_Agée:Worksheet_Change", "", startTime)
  
End Sub
