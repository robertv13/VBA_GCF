Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate()

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshFAC_Brouillon:Worksheet_Activate", 0)

    If fromMenu = False Then
        Debug.Print "#008 - wshFAC_Brouillon_Worksheet_Activate - Je ne viens pas du menu"
        Exit Sub
    End If
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    Dim ws As Worksheet: Set ws = ThisWorkbook.Sheets("FAC_Brouillon")
    
    Call Client_List_Import_All
    Call TEC_Import_All
    Call FAC_Entête_Import_All
    Call FAC_Détails_Import_All
    Call FAC_Comptes_Clients_Import_All
    Call FAC_Projets_Entête_Import_All
    Call FAC_Projets_Détails_Import_All
    
    With ws
        .Unprotect
        .Range("A:B").EntireColumn.Hidden = True
    End With
    
    'Set the zoom factor to 93% when this worksheet is activated
    ActiveWindow.Zoom = 93
    
    Application.Calculation = xlCalculationAutomatic
    
    Call SetTabOrder(ws)
    
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlNoRestrictions
'        .EnableSelection = xlUnlockedCells
    End With
    
    'Ensure all pending events are processed - 2024-07-21 @ 17:16
    DoEvents
    
    Application.EnableEvents = False
    
    Application.Goto Reference:=Me.Cells(1, 3), Scroll:=True
    
    Me.Range("O3").value = Format$(Now(), wshAdmin.Range("B1").value)
    Me.Range("B27").value = False '2024-09-03 @ 06:33

    Me.Range("E3").value = "" '2024-09-02 @ 23:36
    Me.Range("E3").Select 'The first input is the customer name
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    fromMenu = False

    Call FAC_Brouillon_New_Invoice
    
    'Libérer la mémoire
    Set ws = Nothing
    
    Call Log_Record("wshFAC_Brouillon:Worksheet_Activate", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)
    
    Dim startTime As Double: startTime = Timer: Call Log_Record("wshFAC_Brouillon:Worksheet_SelectionChange" & ":" & Target.Address, 0)
    
    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = BASIC_COLOR
    End If
    
    'Masquer la forme (détail TEC) si elle est présente
    On Error Resume Next
    Dim shapeTextBox As Shape
    Set shapeTextBox = Me.Shapes("shpTECInfo")
    If Not shapeTextBox Is Nothing Then
        shapeTextBox.Visible = msoFalse
    End If
    On Error GoTo 0
    
    'Client has been selected
    If Not Intersect(Target, wshFAC_Brouillon.Range("E3")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'Date has been selected
    If Not Intersect(Target, wshFAC_Brouillon.Range("O3")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'WIP description has been selected
    If Not Intersect(Target, wshFAC_Brouillon.Range("F7:F999")) Is Nothing Then
        'Ajouter la forme si elle n'existe pas encore -ET- que le texte est assez long...
        If Target.Cells.count = 1 Then
            If Len(Target.value) > 58 Then
                If shapeTextBox Is Nothing Then
                    Set shapeTextBox = Me.Shapes.AddTextbox(msoTextOrientationHorizontal, _
                                                            100, 100, 600, 100)
                    shapeTextBox.Name = "shpTECInfo"
                End If
                
                'Définir le texte d'information en fonction de la cellule sélectionnée
                Dim infoText As String
                infoText = Target.value
        
                'Mettre à jour et afficher la forme
                With shapeTextBox
                    .TextFrame.Characters.Text = infoText
                    .Top = Target.Top + Target.Height
                    .Left = Target.Left
                    .Visible = msoTrue
                    .Fill.ForeColor.RGB = RGB(255, 255, 204) ' Fond de couleur douce pour visibilité
                    .TextFrame.Characters.Font.size = 10
                End With
            End If
        End If
    End If
    
    'Template has been selected
    If Not Intersect(Target, wshFAC_Brouillon.Range("O9")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If

    'Service description has been selected
    If Not Intersect(Target, wshFAC_Brouillon.Range("L11:L45")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
    End If
    
    'Honoraires has been selected
    If Not Intersect(Target, wshFAC_Brouillon.Range("O47")) Is Nothing Then
        Target.Interior.Color = HIGHLIGHT_COLOR
        wshFAC_Brouillon.Unprotect
    End If

    'Libérer la mémoire
    Set shapeTextBox = Nothing
    
    Call Log_Record("wshFAC_Brouillon:Worksheet_SelectionChange", startTime)

End Sub

Private Sub Worksheet_Change(ByVal Target As Range)

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshFAC_Brouillon:Worksheet_Change" & ":" & Target.Address, 0)
    
    Dim targetAddress As String
    On Error Resume Next
    targetAddress = Target.Address
    On Error GoTo 0
    
    Application.EnableEvents = False
    
    Dim fullDate As Variant
    Dim rng As Range
    
    'Client Name has changed
    If Not Intersect(Target, Me.Range("E3")) Is Nothing And Me.Range("E3").value <> Empty Then
        Call FAC_Brouillon_Client_Change(Me.Range("E3").value)
        Set rng = Me.Range("O3")
    End If
    
    'Invoice Date has changed
    If Not Intersect(Target, Me.Range("O3")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(Target.Text, 30, 15)
        If fullDate <> "Invalid Date" Then
            Target.value = Format$(fullDate, wshAdmin.Range("B1").value)
        Else
            Call MsgBoxInvalidDate("wshFAC_Brouillon_136")
            Application.EnableEvents = False
            Target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(Target.Address)
        End If
       
        'Future date ?
        If CDate(Range("O3").value) > Now() Then
            If MsgBox("En êtes-vous CERTAIN ?", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                Application.EnableEvents = False
                Target.ClearContents
                Application.EnableEvents = True
                Application.Goto Range(Target.Address)
            End If
        End If

        Debug.Print "#009 - Worksheet_Change   wshFAC_Brouillon.Range(""O3"").value = "; wshFAC_Brouillon.Range("O3").value; "   "; TypeName(wshFAC_Brouillon.Range("O3").value)
        Call FAC_Brouillon_Date_Change(CDate(wshFAC_Brouillon.Range("O3").value))
        
        Set rng = wshFAC_Brouillon.Range("O9")
        GoTo ExitSub
    End If
    
    'Template has changed
    If Not Intersect(Target, wshFAC_Brouillon.Range("O9")) Is Nothing Then
        If Target.value <> "" Then
            Call Load_Invoice_Template(Target.value)
        End If
        Set rng = wshFAC_Brouillon.Range("L11")
    End If
    
    'Services entry
    If Not Intersect(Target, Range("L11:L40")) Is Nothing And Range("B24").value = False And Range("B25").value = False Then
        wshFAC_Finale.Range("B" & Target.row + 23).value = "   - " & wshFAC_Brouillon.Range("L" & Target.row).value
        Set rng = wshFAC_Brouillon.Range("M" & Target.row) 'Hours
'        rng.Select
        GoTo ExitSub
    End If
  
    'Hours per service
    If Not Intersect(Target, Range("M11:M45")) Is Nothing And Range("B24").value = False And Range("B25").value = False Then
        wshFAC_Brouillon.Range("N" & Target.row).value = wshAdmin.Range("TauxHoraireFacturation")
        wshFAC_Brouillon.Range("O" & Target.row).value = wshFAC_Brouillon.Range("M" & Target.row).value * wshFAC_Brouillon.Range("N" & Target.row).value
        Set rng = wshFAC_Brouillon.Range("M" & Target.row)
        wshFAC_Finale.Range("C" & Target.row + 23).value = wshFAC_Brouillon.Range("M" & Target.row).value
        wshFAC_Finale.Range("D" & Target.row + 23).value = wshFAC_Brouillon.Range("N" & Target.row).value
        wshFAC_Finale.Range("E" & Target.row + 23).value = wshFAC_Brouillon.Range("O" & Target.row).value

        'MAXIMUM of 30 services lines per Invoice
        If Target.row < NB_MAX_LIGNE_FAC + 11 Then
            wshFAC_Brouillon.Range("L" & Target.row + 1).Select
        Else
            Set rng = wshFAC_Brouillon.Range("O47")
            wshFAC_Brouillon.Range("O47").Select
        End If
        GoTo ExitSub
      End If
    
    'Fees summary has changed
    If Not Intersect(Target, Range("R44:T48")) Is Nothing Then
        wshFAC_Brouillon.Range("B9").value = True
        wshFAC_Brouillon.Range("O47").value = wshFAC_Brouillon.Range("U49").value
    End If
    
    'Miscellaneous Amounts
    If Not Intersect(Target, Range("O48:O50")) Is Nothing And Range("B24").value = False And Range("B25").value = False Then
        With wshFAC_Finale
            'Final Invoice - Sub-total
'            wshFAC_Finale.Range("E69").value = wshFAC_Brouillon.Range("O47").value
            'Final Invoice - Misc charges AND move on to next field
            If Target.row = 48 Then
                Call Log_Record("wshFAC_Brouillon:Worksheet_Change" & ":" & _
                    targetAddress & " - " & wshFAC_Brouillon.Range("O48").value, -1)
                wshFAC_Finale.Range("E70").value = wshFAC_Brouillon.Range("O48").value
                wshFAC_Brouillon.Range("O49").Select
            End If
            If Target.row = 49 Then
                wshFAC_Finale.Range("E71").value = wshFAC_Brouillon.Range("O49").value
                wshFAC_Brouillon.Range("O50").Select
            End If
            If Target.row = 50 Then
                wshFAC_Finale.Range("E72").value = wshFAC_Brouillon.Range("O50").value
                wshFAC_Brouillon.Range("O57").Select
            End If
        End With
        GoTo ExitSub
    End If

    'Deposit Amount
'    If Not Intersect(Target, Range("O57")) Is Nothing Then
'        With wshFAC_Finale
'            'Final Invoice - Deposit
'            wshFAC_Finale.Range("E79").value = wshFAC_Brouillon.Range("O57").value
'            ActiveWindow.SmallScroll Up:=18
'        End With
'        GoTo ExitSub
'    End If
    
ExitSub:

    Application.EnableEvents = True
    
    'Libérer la mémoire
    Set rng = Nothing

    Call Log_Record("wshFAC_Brouillon:Worksheet_Change" & ":" & targetAddress, startTime)
  
End Sub
