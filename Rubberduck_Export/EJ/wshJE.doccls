Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)

    'Si la cellule de la source change, alors on vérifie si l'on essaie d'appeler des E/J récurrentes
    If Not Intersect(Target, Range("E4")) Is Nothing Then
        Application.EnableEvents = False
        Dim Source As String
        Source = UCase(Trim(Range("E4").Value))
        If Source = "AUTO" Then
            usfListeEJAuto.Show vbModal
        End If
        Unload usfListeEJAuto
        Application.EnableEvents = True
    End If
    
    'Si la cellule de la date change, alors on essaie de construire une date
    If Not Intersect(Target, Range("J4")) Is Nothing Then
        Application.EnableEvents = False
        Dim DateCell As String
        DateCell = Trim(Range("J4").Value)
        Dim r As Range
        Set r = Range("J4")
        Call BuildDate(DateCell, r)
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, sauvegarde du no de compte & suggestion du montant résiduel
    If Not Intersect(Target, Range("D9:F23")) Is Nothing Then
        If (Range("H25").Value - Range("G25").Value) <> 0 Then
            If (Range("H25").Value - Range("G25").Value) > 0 Then
                Range("G" & Target.row).Value = Range("H25").Value - Range("G25").Value
            Else
                Range("H" & Target.row).Value = -(Range("H25").Value - Range("G25").Value)
            End If
        End If
        'Obtenir le numéro de compte à partir de la commande VLOOKUP
        If wshJE.Range("D" & Target.row).Value <> "" Then
            wshJE.Range("K" & Target.row).Value = WorksheetFunction.Vlookup(wshJE.Range("D" & Target.row).Value, wshPlanComptable.Range("$A$3:$B$62"), 2, False)
        Else
            wshJE.Range("K" & Target.row).Value = ""
        End If
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(Target, Range("G9:H23")) Is Nothing Then
        If (wshJE.Range("G" & Target.row).Value < 0 Or wshJE.Range("H" & Target.row).Value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H25").Value <> Range("G25").Value Then
            With Range("G25:H25").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("G25:H25").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
    End If
End Sub
