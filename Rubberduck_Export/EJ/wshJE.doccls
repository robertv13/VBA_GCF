Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)

    'Si la cellule de la date change, alors on essaie de construire une date
    If Not Intersect(Target, Range("J4")) Is Nothing Then
        Application.EnableEvents = False
        Dim cell As String
        cell = Trim(Range("J4").Value)
        Call BuildDate(cell)
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, suggestion du montant résiduel
    If Not Intersect(Target, Range("D9:D23")) Is Nothing And (Range("H25").Value - Range("G25").Value) <> 0 Then
        If (Range("H25").Value - Range("G25").Value) > 0 Then
            Range("G" & Target.row).Value = Range("H25").Value - Range("G25").Value
        Else
            Range("H" & Target.row).Value = -(Range("H25").Value - Range("G25").Value)
        End If
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(Target, Range("G9:H23")) Is Nothing Then
        If (wshJE.Range("G" & Target.row).Value < 0 Or wshJE.Range("H" & Target.row).Value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H25").Value <> Range("G25").Value Then
            With Range("G25:H25").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("G25:H25").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
    End If
    
End Sub

Sub BuildDate(cell As String)
        Dim d, m, y As Integer
        Dim strDateJour, strDateConsruite As String
        Dim dateValide As Boolean
        dateValide = True

        cell = Replace(cell, "/", "")
        cell = Replace(cell, "-", "")

        'Utilisation de la date du jour
        strDateJour = Now
        d = Day(strDateJour)
        m = Month(strDateJour)
        y = Year(strDateJour)

        Select Case Len(cell)
            Case 0
                strDateConsruite = Format(d, "00") & "/" & Format(m, "00") & "/" & Format(y, "0000")
            Case 1, 2
                strDateConsruite = Format(cell, "00") & "/" & Format(m, "00") & "/" & Format(y, "0000")
            Case 3
                strDateConsruite = Format(Left(cell, 1), "00") & "/" & Format(Mid(cell, 2, 2), "00") & "/" & Format(y, "0000")
            Case 4
                strDateConsruite = Format(Left(cell, 2), "00") & "/" & Format(Mid(cell, 3, 2), "00") & "/" & Format(y, "0000")
            Case 6
                strDateConsruite = Format(Left(cell, 2), "00") & "/" & Format(Mid(cell, 3, 2), "00") & "/" & "20" & Format(Mid(cell, 5, 2), "00")
            Case Else
                dateValide = False
        End Select
        If IsDate(strDateConsruite) = False Then dateValide = False

    If dateValide Then
        Range("J4").Value = strDateConsruite
    Else
        MsgBox "La saisie est invalide...", vbInformation, "Il est impossible de construire une date"
    End If

End Sub
