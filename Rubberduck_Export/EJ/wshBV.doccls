Option Explicit

Private Sub Worksheet_Change(ByVal Target As Range)

    If Not Intersect(Target, Range("J1")) Is Nothing Then
        Application.EnableEvents = False
        Dim cell As String
        cell = Trim(Range("J1").Value)
        Dim r As Range
        Set r = Range("J1")
        Call BuildDate(cell, r)
        wshBV.Range("B1").Value = wshBV.Range("J1").Value 'Mise à jour des contrôles
        Application.EnableEvents = True
    End If

End Sub

Private Sub Worksheet_SelectionChange(ByVal Target As Range)

    If Not Intersect(Target, wshBV.Range("D4:G" & Range("B2").Value)) Is Nothing Then
        Application.EnableEvents = False
        Dim GLAcct As String, GLDescription As String, DateLimite As String
        GLAcct = CStr(Range("D" & Target.Row).Value)
        GLDescription = Range("E" & Target.Row).Value
        DateLimite = Format(Range("B1").Value, "yyyymmdd")
        GLTransDisplay GLAcct, GLDescription, DateLimite
        Application.EnableEvents = True
    End If

End Sub

Sub UpdateBV() 'Forme 'Actualiser'
    Dim dateCutOff As String
    Dim Row As Long, rowUsed As Long
    Dim LastRow As Long, startRow As Long, MyRow As Long
    Dim Solde As Currency
    
    startRow = 3  'your first row of actual data in Plan Comptable
    LastRow = wshPlanComptable.Cells(Rows.Count, "E").End(xlUp).Row
    Row = 4       'Première ligne dans la Balance de Vérification
    
    'Efface la BV + Détail du GL
    wshBV.Range("D" & Row & ":G" & LastRow + 4).ClearContents
    wshBV.Range("D" & Row & ":G" & LastRow + 4).ClearFormats
    Dim rowGLDetailLast As Long
    rowGLDetailLast = wshBV.Range("M99999").End(xlUp).Row 'Dernière ligne utilisée
    If rowGLDetailLast < 4 Then rowGLDetailLast = 4
    wshBV.Range("L4:T" & rowGLDetailLast).ClearContents
    wshBV.Range("R" & rowGLDetailLast & ":R" & rowGLDetailLast).Font.Bold = False
    With wshBV.Range("R5:R99999").Interior
        .Pattern = xlNone
        .TintAndShade = 0
        .PatternTintAndShade = 0
    End With
    
    dateCutOff = Format(wshBV.Range("J1").Value, "yyyymmdd")
    
    For MyRow = startRow To LastRow
        Solde = GetBalance(wshPlanComptable.Cells(MyRow, 5), dateCutOff)
        If Solde <> 0 Then
            wshBV.Range("D" & Row).Value = wshPlanComptable.Cells(MyRow, 5)
            wshBV.Range("E" & Row).Value = wshPlanComptable.Cells(MyRow, 6)
            If Solde > 0 Then
                wshBV.Range("F" & Row).Value = Solde
            Else
                wshBV.Range("G" & Row).Value = -Solde
            End If
            Row = Row + 1
            rowUsed = rowUsed + 1
        End If
    Next

    setTotals Row + 1, 6
    setTotals Row + 1, 7
  
End Sub

Sub setTotals(r As Long, c As Long)

    With wshBV.Cells(r, c).Borders(xlEdgeTop)
        .LineStyle = xlContinuous
        .ColorIndex = 0
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With wshBV.Cells(r, c).Borders(xlEdgeBottom)
        .LineStyle = xlContinuous
        .ColorIndex = 0
        .TintAndShade = 0
        .Weight = xlMedium
    End With
    wshBV.Cells(r, c).Font.Bold = True
    
    'Ajoute la formule SUM() aux colonnes Débit et Crédit
    wshBV.Range("F" & r).Formula = "=SUM(F4:F" & r - 1 & ")"
    wshBV.Range("G" & r).Formula = "=SUM(G4:G" & r - 1 & ")"
    
    'Force le format de la colonne F & G
    wshBV.Range("F" & r).NumberFormat = "#,##0.00 $"
    wshBV.Range("G" & r).NumberFormat = "#,##0.00 $"
    wshBV.Range("B2").Value = r - 2

End Sub

Function GetBalance(GL As String, d As String) As Currency

    Dim lastRowTrans, startRow, rowTrans As Long
    Dim Solde As Currency
    
    lastRowTrans = wshGL.Cells(Rows.Count, "D").End(xlUp).Row
    startRow = 2

    For rowTrans = startRow To lastRowTrans
        If wshGL.Range("G" & rowTrans).Value = GL And Format(wshGL.Range("D" & rowTrans).Value, "yyyymmdd") <= d Then
            GetBalance = GetBalance + wshGL.Range("I" & rowTrans).Value - wshGL.Range("J" & rowTrans).Value
        End If
    Next
End Function

Sub GLTransDisplay(GLAcct As String, GLDesc As String, d As String)

    'Efface le tableau
    Dim rowGLDetail As Long, rowGLDetailMax As Long
    rowGLDetail = 4
    rowGLDetailMax = wshBV.Range("M99999").End(xlUp).Row
    If rowGLDetailMax < 4 Then rowGLDetailMax = 4
    wshBV.Range("L" & rowGLDetail & ":T" & rowGLDetailMax).ClearContents
    wshBV.Range("R" & rowGLDetailMax & ":R" & rowGLDetailMax).Font.Bold = False
    With wshBV.Range("R" & rowGLDetailMax & ":R" & rowGLDetailMax).Interior
        .Pattern = xlNone
        .TintAndShade = 0
        .PatternTintAndShade = 0
    End With
    
    'Affiche le compte et la description
    wshBV.Range("L" & rowGLDetail).Value = GLAcct & " - " & GLDesc
    rowGLDetail = rowGLDetail + 1
    
    'Analyse des transactions détaillées
    Dim r As Long, RowGLFirst As Long, RowGLMax As Long
    RowGLFirst = 2
    RowGLMax = wshGL.Range("C999999").End(xlUp).Row  'Last Row used in wshGL (Transactions)
    
    For r = RowGLFirst To RowGLMax
        If wshGL.Range("G" & r).Value = GLAcct And Format(wshGL.Range("D" & r).Value, "yyyymmdd") <= d Then
            wshBV.Range("M" & rowGLDetail).Value = wshGL.Range("D" & r).Value
            wshBV.Range("N" & rowGLDetail).Value = wshGL.Range("E" & r).Value
            wshBV.Range("O" & rowGLDetail).Value = wshGL.Range("F" & r).Value
            wshBV.Range("P" & rowGLDetail).Value = wshGL.Range("I" & r).Value
            wshBV.Range("Q" & rowGLDetail).Value = wshGL.Range("J" & r).Value
            wshBV.Range("S" & rowGLDetail).Value = wshGL.Range("K" & r).Value
            wshBV.Range("T" & rowGLDetail).Value = wshGL.Range("L" & r).Value
            rowGLDetail = rowGLDetail + 1
        End If
    Next
    
    Dim rowGLDetailLast As Long
    rowGLDetailLast = rowGLDetail - 1
    
    'Tri des données affichées, s'il y a des données...
    If rowGLDetailLast < 5 Then Exit Sub
    
    wshBV.Range("M5:T" & rowGLDetailLast).Sort _
        key1:=[M5], order1:=xlAscending, _
        key2:=[N5], order2:=xlAscending, _
        Key3:=[T5], order3:=xlAscending

    wshBV.Range("R4").Value = 0
    rowGLDetailMax = wshBV.Range("M99999").End(xlUp).Row
    For r = 5 To rowGLDetailMax
        wshBV.Range("R" & r).Value = wshBV.Range("R" & r - 1).Value + wshBV.Range("P" & r).Value - wshBV.Range("Q" & r).Value
        If r = 5 Or r <> rowGLDetailMax Then
        wshBV.Range("R" & r).Font.Bold = False
        End If
    Next r
    wshBV.Range("R" & r - 1).Font.Bold = True
    With wshBV.Range("R" & r - 1).Interior
        .Pattern = xlSolid
        .PatternColorIndex = xlAutomatic
        .ThemeColor = xlThemeColorDark1
        .TintAndShade = -4.99893185216834E-02
        .PatternTintAndShade = 0
    End With

End Sub