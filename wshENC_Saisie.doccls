Option Explicit

Public previousCellAddress As Variant
Public clientCode As String
Public pmtNo As Long
Public GLEntryNo As Long

Private Sub Worksheet_Activate()

    If fromMenu = False Then
        Debug.Print "#007 - wshENC_Saisie_Worksheet_Activate - Je ne viens pas du menu"
        Exit Sub
    End If
    
    Dim startTime As Double: startTime = Timer: Call Log_Record("wshENC_Saisie:Worksheet_Activate", "", 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call Client_List_Import_All
    Call FAC_Comptes_Clients_Import_All
    Call FAC_Détails_Import_All
    Call FAC_Entête_Import_All
    Call ENC_Détails_Import_All
    Call ENC_Entête_Import_All
    Call GL_Trans_Import_All
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100
    
    Dim ws As Worksheet: Set ws = wshENC_Saisie
    
    Me.Application.Calculation = xlCalculationAutomatic
    
    On Error Resume Next
    Me.Columns("A:B").Hidden = True
    On Error GoTo 0
    
    'S'assurer que les cellules qui auraient été déverouillées le soient pour commencer
    On Error Resume Next
    ws.Range("B12:B36").Locked = True
    ws.Range("E12:E36").Locked = True
    On Error GoTo 0
    
    'Certaines cellules perdent la couleur du background
    Dim cellsToColor As Range
    Set cellsToColor = Union(ws.Range("E5"), ws.Range("I5"), ws.Range("F6"), ws.Range("G7"))
    Call Remplir_Plage_Avec_Couleur(cellsToColor, COULEUR_BASE_COMPTABILITÉ)
    
    'Protect the Worksheet
    With ws
        .Protect UserInterfaceOnly:=True
        .EnableSelection = xlUnlockedCells
    End With
    
    Call SetTabOrder(ws)
    
    Call ENC_Remove_Check_Boxes(36)
    
    'Efface les données du bordereau total
    Dim lastUsedBordereau As Long
    lastUsedBordereau = ws.Cells(ws.Rows.count, "O").End(xlUp).row
    If lastUsedBordereau < 6 Then
        lastUsedBordereau = 6
    End If
    ws.Range("O6:Q" & lastUsedBordereau + 2).Clear
    
    Call AjusteLibelléEncaissement("Banque")
    
    With ws
        .Range("K5").Value = Format$(Date, wshAdmin.Range("B1").Value)
        .Range("B1").Select
    End With

    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    wshENC_Saisie.Range("F5:H5").Select
    
    'Libérer la mémoire
    Set ws = Nothing
    
    Call Log_Record("wshENC_Saisie:Worksheet_Activate", "", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal target As Range)
    
    If previousCellAddress <> "" Then
        Debug.Print "#097 - Previous = " & previousCellAddress & " - Current = " & target.Address
        Range(previousCellAddress).Interior.Color = COULEUR_BASE_COMPTABILITÉ
    End If
    
    'Client has been selected
    If Not Intersect(target, Me.Range("F5")) Is Nothing Then
'        MsgBox "Cellule F5 sélectionnée, prêt pour la saisie.", vbInformation
        target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Date has been selected
    If Not Intersect(target, Me.Range("K5")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Payment Type has been selected
    If Not Intersect(target, Me.Range("F7")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Total deposit has been selected
    If Not Intersect(target, Me.Range("K7")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If

    'Comments has been selected
    If Not Intersect(target, Me.Range("F9")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Première case à cocher a été sélectionnée
    If Not Intersect(target, Me.Range("E12")) Is Nothing Then
        Call ValiderEtLancerufEncRégularisation
    End If
    
    'Ne devrait pas passer ici...
    If Not Intersect(target, Me.Range("I5")) Is Nothing Then
        target.Interior.Color = COULEUR_BASE_COMPTABILITÉ
        Me.Range("K5").Select
        Exit Sub
    End If
    
    If Not Intersect(target, Me.Range("G7")) Is Nothing Then
        target.Interior.Color = COULEUR_BASE_COMPTABILITÉ
        Me.Range("K7").Select
        Exit Sub
    End If
    
    previousCellAddress = target.Address
    
End Sub

Private Sub Worksheet_Change(ByVal target As Range)
    
    Dim fullDate As Variant
    
    'On Customer Change and only on new payments
    If Not Intersect(target, Me.Range("F5")) Is Nothing And _
        Me.Range("F5").Value <> Empty And _
        Me.Range("B6").Value = "" Then
        'Aller chercher le vrai nom du client
        Dim allCols As Variant
        allCols = Fn_Get_A_Row_From_A_Worksheet("BD_Clients", target.Value, fClntFMNomClientPlusNomClientSystème)
        'Vérifier les résultats
        If IsArray(allCols) Then
            Application.EnableEvents = False
            target.Value = allCols(1)
            Application.EnableEvents = True
        Else
            MsgBox "Valeur non trouvée !!!", vbCritical
        End If
        Application.EnableEvents = False
        clientCode = allCols(fClntFMClientID)
        Call ENC_Remove_Check_Boxes(36)
        Call ENC_Get_OS_Invoices(clientCode)
        Application.EnableEvents = True
    End If
    
    'Date has changed
    If Not Intersect(target, Me.Range("K5")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(target.Text, 30, 0)
        If fullDate <> "Invalid Date" Then
            target.Value = Format$(fullDate, wshAdmin.Range("B1").Value)
        Else
            Call modTEC_Saisie.AfficherMessageDateInvalide("wshENC_Saisie_115")
            target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(target.Address)
        End If
        target.Interior.Color = COULEUR_BASE_COMPTABILITÉ
        Application.EnableEvents = True
    End If
    
    'Type a changé
    If Not Intersect(target, Me.Range("F7")) Is Nothing Then
        Call AjusteLibelléEncaissement(target.Value)
    End If
    
End Sub

