Option Explicit

Public previousCellAddress As Variant

Private Sub Worksheet_Activate() '2024-06-13 @ 18:07

    Dim startTime As Double: startTime = Timer: Call Log_Record("wshGL_EJ:Worksheet_Activate", "", 0)
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    
    'Import transactions from MASTER file
    Call GL_Trans_Import_All
    Call GL_EJ_Recurrente_Import_All
    
    'Set the zoom factor to 100% when this worksheet is activated
    ActiveWindow.Zoom = 100

    Me.Application.Calculation = xlCalculationAutomatic

    Dim ws As Worksheet: Set ws = wshGL_EJ
    
   'Hide column A & B
    With ws
        .Unprotect
        .Range("A:B").EntireColumn.Hidden = True
        .Range("N:Y").EntireColumn.Hidden = True
    End With
    
    'Cette cellule peut aussi contenir "Client:"
    ws.Range("E6").Value = "Description:"
    
    Call SetTabOrder(ws)
    
    Call GL_EJ_Clear_All_Cells
    
    'Rétablir le bouton "Mettre à jour / Renversement"
    Dim shp As Shape
    Set shp = ws.Shapes("btnUpdate")
    
    shp.Left = 530
    shp.Width = 115
    shp.Height = 30
    shp.TextFrame2.TextRange.Text = "Mettre à jour"
    shp.TextFrame2.TextRange.Font.Fill.ForeColor.RGB = RGB(0, 0, 0) 'Noir
    
    Call Sauvegarder_Forme(shp)
    
    With ws
        .Range("B2").Value = -1
        .Range("B3").Value = -1
        .Range("K4").Value = Format$(Date, wshAdmin.Range("B1").Value)
        .Range("F4").Select
        previousCellAddress = .Range("F4").Address
    End With
    
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    
    'Libérer la mémoire
    Set shp = Nothing
    Set ws = Nothing
    
    Call Log_Record("wshGL_EJ:Worksheet_Activate", "", startTime)

End Sub

Private Sub Worksheet_SelectionChange(ByVal target As Range)

    If previousCellAddress <> "" Then
        Range(previousCellAddress).Interior.Color = xlNone
    End If

    'Ne pas permettre aucun changement à l'écriture originale, lors d'un renversement
    If wshGL_EJ.Range("F4").Value = "Renversement" Then
        MsgBox "Il n'est pas permis de modifier l'écriture à renverser", vbInformation, ""
        Exit Sub
    End If
    
    'Source has been selected
    If Not Intersect(target, wshGL_EJ.Range("F4")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If

    'Date has been selected
    If Not Intersect(target, wshGL_EJ.Range("K4")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If

    'Description has been selected
    If Not Intersect(target, wshGL_EJ.Range("F6")) Is Nothing Then
        If wshGL_EJ.Range("F4").Value = "Dépôt de client" Then
            MsgBox "Vous devez maintenant saisir le nom du client" & _
                vbNewLine & vbNewLine & "qui effectue le dépôt de client (perçu d'avance)", _
                vbOKOnly, "Enregistrement d'un dépôt de client"
        End If
        target.Interior.Color = COULEUR_SAISIE
    End If

    'Compte has been selected
    If Not Intersect(target, wshGL_EJ.Range("E9:E23")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
        If target.row = 9 And wshGL_EJ.Range("F4").Value = "Remise TPS/TVQ" Then
            Call GL_EJ_Construire_Remise_TPS_TVQ(target.row)
        End If
    End If
    
    'Debit amount has been selected
    If Not Intersect(target, wshGL_EJ.Range("H9:H23")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Credit amount has been selected
    If Not Intersect(target, wshGL_EJ.Range("I9:I23")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Extra description has been selected
    If Not Intersect(target, wshGL_EJ.Range("J9:J23")) Is Nothing Then
        target.Interior.Color = COULEUR_SAISIE
    End If
    
    'Save the current cell Address
    previousCellAddress = target.Address

End Sub

Private Sub Worksheet_Change(ByVal target As Range)

    Dim codeClient As Variant
    Dim fullDate As Variant
    Dim plageClients As Range
    
    'Si la cellule de la SOURCE change, alors on vérifie si l'on essaie...
    '   E/J récurrente
    '   Renverser une écriture
    '   Dépôt de client
    If Not Intersect(target, Range("F4")) Is Nothing Then
        If target.CountLarge > 1 Then Exit Sub
        Application.EnableEvents = False
            If UCase(Trim(Range("F4").Value)) = "AUTO" Then
                wshGL_EJ.Range("B2").Value = -1
                ufListeEJAuto.show
                If wshGL_EJ.Range("B2").Value >= 0 Then
                    wshGL_EJ.Range("K4").Activate
                    wshGL_EJ.Range("K4").Select
                Else
                    Call GL_EJ_Clear_All_Cells
                End If
            End If
        Application.EnableEvents = True
        If target.Value = "Renversement" Then
            Call GL_EJ_Renverser_Ecriture
        End If
        If target.Value = "Dépôt de client" Then
            Call GL_EJ_Depot_Client
        End If
    End If
    
    'Description -OU- Nom du client a changé
    If Not Intersect(target, Range("F6")) Is Nothing Then
        If wshGL_EJ.Range("F4").Value = "Dépôt de client" Then
            Application.EnableEvents = False
            Dim allCols As Variant
            allCols = Fn_Get_A_Row_From_A_Worksheet("BD_Clients", target.Value, fClntFMNomClientPlusNomClientSystème)
            'Vérifier les résultats
            If IsArray(allCols) Then
                Application.EnableEvents = False
                target.Value = allCols(1) 'Ramener le nom du client - Colonne 1
                Application.EnableEvents = True
            Else
                MsgBox "Valeur non trouvée !!!", vbCritical
            End If
            Application.EnableEvents = False
            Dim clientCode As String
            clientCode = allCols(fClntFMClientID)
            Me.Range("B6").Value = clientCode
            Application.EnableEvents = True
            End If
    End If
    
    'Si la cellule de la DATE change, alors on essaie de construire une date
    If Not Intersect(target, Range("K4")) Is Nothing Then
        Application.EnableEvents = False
        fullDate = Fn_Complete_Date(target.Text, 999, 15)
        
        'Update the cell with the full date, if valid
        If fullDate <> "Invalid Date" Then
            target.Value = Format$(fullDate, wshAdmin.Range("B1").Value)
        Else
            Call modTEC_Saisie.AfficherMessageDateInvalide("wshGL_EJ_123")
            Application.EnableEvents = False
            target.ClearContents
            Application.EnableEvents = True
            Application.Goto Range(target.Address)
        End If
        
        If CDate(Me.Range("K4").Value) > Date Then
            If MsgBox("En êtes-vous CERTAIN ?", vbYesNo + vbCritical, "Utilisation d'une date FUTURE") = vbNo Then
                target.Value = ""
                target.Activate
                target.Select
            End If
        End If
        Application.EnableEvents = True
    End If
    
    'Modification de la description du compte, sauvegarde du no de compte & suggestion du montant résiduel
    If Not Intersect(target, Range("E9:G23")) Is Nothing Then
        Application.EnableEvents = False
        With wshGL_EJ
            If .Range("E" & target.row).Value <> "" Then
                .Range("L" & target.row).Value = Fn_GetGL_Code_From_GL_Description(.Range("E" & target.row).Value)
            End If

            If target.row > 9 And wshGL_EJ.Range("F4").Value <> "Remise TPS/TVQ" And _
                (.Range("H26").Value <> 0 Or .Range("I26").Value <> 0) And _
                .Range("H26").Value <> .Range("I26").Value Then
                    If .Range("H26").Value > .Range("I26").Value Then
                        .Range("I" & target.row).Value = .Range("H26").Value - .Range("I26").Value
                        Application.EnableEvents = True
                        .Range("I" & target.row).Select
                        Application.EnableEvents = False
                    Else
                        .Range("H" & target.row).Value = .Range("I26").Value - .Range("H26").Value
                        Application.EnableEvents = True
                        .Range("H" & target.row).Select
                        Application.EnableEvents = False
                    End If
            End If
        End With
        Application.EnableEvents = True
    End If
    
    'Validation du montant saisi (Débit ou Crédit), ne peut être négatif
    If Not Intersect(target, Range("H9:I23")) Is Nothing Then
        If (wshGL_EJ.Range("H" & target.row).Value < 0 Or wshGL_EJ.Range("I" & target.row).Value < 0) Then
            MsgBox "Montant INVALIDE, ne peut être NÉGATIF", vbInformation, "Montant négatif saisi"
            Exit Sub
        End If
        If Range("H26").Value <> Range("I26").Value Then
            With Range("H26:I26").Interior
                .Pattern = xlSolid
                .PatternColorIndex = xlAutomatic
                .Color = 65535
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        Else
            With Range("H26:I26").Interior
                .Pattern = xlNone
                .TintAndShade = 0
                .PatternTintAndShade = 0
            End With
        End If
    End If
End Sub
